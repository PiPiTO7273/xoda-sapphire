
#module _MusicSelect_

	//DXライブラリグラフィック(musicselect)
	#enum ilayer_all	=	0
	#enum ibg
	#enum iwindow_category
	#enum iwindow_songs
	#enum ifolder_all
	#enum ifolder_rank_ex
	#enum ifolder_rank_s
	#enum ifolder_rank_aaa
	#enum ifolder_rank_aa
	#enum ifolder_rank_a
	#enum ifolder_rank_b
	#enum ifolder_rank_c
	#enum ifolder_allex
	#enum ifolder_fullcombo
	#enum ifolder_madness
	#enum ifolder_expert
	#enum ifolder_normal
	#enum ifolder_assist
	#enum ibg_window
	#enum inav
	#enum ibg_item
	#enum icursor_category
	#enum icursor_list
	#enum ibg_meta
	#enum ibg_detail
	#enum ilayer_nodata
	#enum ibutton_start_0
	#enum ibutton_start_1
	#enum ibutton_start_2
	#enum ibutton_normal_0
	#enum ibutton_normal_1
	#enum ibutton_normal_2
	#enum ibutton_hard_0
	#enum ibutton_hard_1
	#enum ibutton_hard_2
	#enum ibutton_chaos_0
	#enum ibutton_chaos_1
	#enum ibutton_chaos_2

	//BASS Audio Libraryサンプル、チャンネルハンドル(musicselect)
	#enum bgmMain	=	0
	#enum sndCancel
	#enum sndChange_0
	#enum sndCursor
	#enum sndDecide
	#enum sndError
	#enum sndNotification
	#enum sndText

	//DXライブラリフォントハンドル(musicselect)
	#enum fEOEXB16			=	0
	#enum fEOEXB20
	#enum fFAB50
	#enum fNSCJM12
	#enum fNSCJM15
	#enum fNSCJM16
	#enum fNSCJM20
	#enum fNSCJM24
	#enum fNSCJM32
	#enum fPS14
	#enum fPS16
	#enum fPS18
	#enum fPS20
	#enum fSQNUM16
	#enum fSQNUM24
	#enum fSQNUM40

	//ホイール対応ウィンドウ
	#enum ID_CATEGORY	=	0
	#enum ID_SONGS

	//判定データの識別ID
	#enum ID_DATA_EXCELLENT	=	0
	#enum ID_DATA_GREAT
	#enum ID_DATA_GOOD
	#enum ID_DATA_BAD
	#enum ID_DATA_MISS
	#enum ID_DATA_EARLY
	#enum ID_DATA_LATE
	#enum ID_DATA_RATIO

	//譜面フィルター
	#enum ID_CAT_ALL		=	0
	#enum ID_CAT_RANK_EX
	#enum ID_CAT_RANK_S
	#enum ID_CAT_RANK_AAA
	#enum ID_CAT_RANK_AA
	#enum ID_CAT_RANK_A
	#enum ID_CAT_RANK_B
	#enum ID_CAT_RANK_C
	#enum ID_CAT_TYPE_ALLEX
	#enum ID_CAT_TYPE_FC
	#enum ID_CAT_TYPE_MADNESS
	#enum ID_CAT_TYPE_EXPERT
	#enum ID_CAT_TYPE_CLEAR
	#enum ID_CAT_TYPE_ASSIST

	//個数
	#const NUM_ITEM_CATEGORY		14

	#deffunc LoadMusicSelectImages
		DrawLoadScreen"しばらくお待ち下さい",130
		dim hdximg
		SetDXArchiveExtension"bin"
		hdximg(ilayer_all)=MakeScreen(BufWidth@,BufHeight@,TRUE)
		hdximg(ibg)=LoadGraph("Data/bg_Main.ogv")
		hdximg(ifolder_all)=LoadGraph("Data/res_musicselect_item/category_all.png")
		hdximg(ifolder_rank_ex)=LoadGraph("Data/res_musicselect_item/category_rank_ex.png")
		hdximg(ifolder_rank_s)=LoadGraph("Data/res_musicselect_item/category_rank_s.png")
		hdximg(ifolder_rank_aaa)=LoadGraph("Data/res_musicselect_item/category_rank_aaa.png")
		hdximg(ifolder_rank_aa)=LoadGraph("Data/res_musicselect_item/category_rank_aa.png")
		hdximg(ifolder_rank_a)=LoadGraph("Data/res_musicselect_item/category_rank_a.png")
		hdximg(ifolder_rank_b)=LoadGraph("Data/res_musicselect_item/category_rank_b.png")
		hdximg(ifolder_rank_c)=LoadGraph("Data/res_musicselect_item/category_rank_c.png")
		hdximg(ifolder_allex)=LoadGraph("Data/res_musicselect_item/category_allex.png")
		hdximg(ifolder_fullcombo)=LoadGraph("Data/res_musicselect_item/category_fullcombo.png")
		hdximg(ifolder_madness)=LoadGraph("Data/res_musicselect_item/category_madness.png")
		hdximg(ifolder_expert)=LoadGraph("Data/res_musicselect_item/category_expert.png")
		hdximg(ifolder_normal)=LoadGraph("Data/res_musicselect_item/category_normal.png")
		hdximg(ifolder_assist)=LoadGraph("Data/res_musicselect_item/category_assist.png")
		hdximg(ibg_window)=LoadGraph("Data/bg_musicselect_window.png")
		hdximg(inav)=LoadGraph("Data/Nav_MusicSelect.png")
		hdximg(ibg_item)=LoadGraph("Data/res_musicselect_item/bg_item.png")
		hdximg(icursor_category)=LoadGraph("Data/cursor_musicselect_category.png")
		hdximg(icursor_list)=LoadGraph("Data/cursor_musicselect_list.png")
		hdximg(ibg_meta)=LoadGraph("Data/bg_musicselect_metadata.png")
		hdximg(ibg_detail)=LoadGraph("Data/bg_musicselect_detail.png")
		hdximg(ilayer_nodata)=LoadGraph("Data/layer_musicselect_nodata.png")
		hdximg(ibutton_start_0)=LoadGraph("Data/res_musicselect_button_start/0.png")
		hdximg(ibutton_start_1)=LoadGraph("Data/res_musicselect_button_start/1.png")
		hdximg(ibutton_start_2)=LoadGraph("Data/res_musicselect_button_start/2.png")
		hdximg(ibutton_normal_0)=LoadGraph("Data/res_musicselect_button_difficulty/normal_0.png")
		hdximg(ibutton_normal_1)=LoadGraph("Data/res_musicselect_button_difficulty/normal_1.png")
		hdximg(ibutton_normal_2)=LoadGraph("Data/res_musicselect_button_difficulty/normal_2.png")
		hdximg(ibutton_hard_0)=LoadGraph("Data/res_musicselect_button_difficulty/hard_0.png")
		hdximg(ibutton_hard_1)=LoadGraph("Data/res_musicselect_button_difficulty/hard_1.png")
		hdximg(ibutton_hard_2)=LoadGraph("Data/res_musicselect_button_difficulty/hard_2.png")
		hdximg(ibutton_chaos_0)=LoadGraph("Data/res_musicselect_button_difficulty/chaos_0.png")
		hdximg(ibutton_chaos_1)=LoadGraph("Data/res_musicselect_button_difficulty/chaos_1.png")
		hdximg(ibutton_chaos_2)=LoadGraph("Data/res_musicselect_button_difficulty/chaos_2.png")

		hdximg(iwindow_category)=MakeScreen(450,811,TRUE)
		hdximg(iwindow_songs)=MakeScreen(700,811,TRUE)
	return

	#deffunc LoadMusicSelectSounds
		DrawLoadScreen"しばらくお待ち下さい",250
		dim64 hbasschannel:dim64 hbasssample
		LoadBASSsample"Data/bgm_MusicSelect.ogg",hbasschannel(bgmMain),hbasssample(bgmMain),BASS_SAMPLE_LOOP
		LoadBASSsample"Data/snd_cancel.wav",hbasschannel(sndCancel),hbasssample(sndCancel),NULL
		LoadBASSsample"Data/snd_change_0.wav",hbasschannel(sndChange_0),hbasssample(sndChange_0),NULL
		LoadBASSsample"Data/snd_cursor.wav",hbasschannel(sndCursor),hbasssample(sndCursor),NULL
		LoadBASSsample"Data/snd_decide.wav",hbasschannel(sndDecide),hbasssample(sndDecide),NULL
		LoadBASSsample"Data/snd_error.wav",hbasschannel(sndError),hbasssample(sndError),NULL
		LoadBASSsample"Data/snd_notification.wav",hbasschannel(sndNotification),hbasssample(sndNotification),NULL
		LoadBASSsample"Data/snd_text.wav",hbasschannel(sndText),hbasssample(sndText),NULL
	return

	//曲選択画面で使用するフォントの初期化
	#deffunc InitMusicSelectFonts
		DrawLoadScreen"しばらくお待ち下さい",300
		InitFontToHandle
		dim hdxfont
		hdxfont(fEOEXB16)=LoadFontDataToHandle("Data/eoexb16.bin")
		hdxfont(fEOEXB20)=LoadFontDataToHandle("Data/eoexb20.bin")
		hdxfont(fFAB50)=LoadFontDataToHandle("Data/fab50.bin")
		hdxfont(fNSCJM12)=LoadFontDataToHandle("Data/nscjm12.bin")
		hdxfont(fNSCJM15)=LoadFontDataToHandle("Data/nscjm15.bin")
		hdxfont(fNSCJM16)=LoadFontDataToHandle("Data/nscjm16.bin")
		hdxfont(fNSCJM20)=LoadFontDataToHandle("Data/nscjm20.bin")
		hdxfont(fNSCJM24)=LoadFontDataToHandle("Data/nscjm24.bin")
		hdxfont(fNSCJM32)=LoadFontDataToHandle("Data/nscjm32.bin")
		hdxfont(fPS14)=LoadFontDataToHandle("Data/ps14.bin")
		hdxfont(fPS16)=LoadFontDataToHandle("Data/ps16.bin")
		hdxfont(fPS18)=LoadFontDataToHandle("Data/ps18.bin")
		hdxfont(fPS20)=LoadFontDataToHandle("Data/ps20.bin")
		hdxfont(fSQNUM40)=LoadFontDataToHandle("Data/sqnum40.bin")
		hdxfont(fSQNUM24)=LoadFontDataToHandle("Data/sqnum24.bin")
		hdxfont(fSQNUM16)=LoadFontDataToHandle("Data/sqnum16.bin")
	return

	//IRを同期
	#deffunc IRsync
		DrawLoadScreen"しばらくお待ち下さい",400
		sdim IRsyncbuf
		IRsyncbuf=WebAPI_GetRecord()
		return

	//ライブラリをスキャン
	#define global ScanAllPlaylist(%1=0) _ScanAllPlaylist %1
	#deffunc _ScanAllPlaylist int _filter

		//一度全ての項目をスキャン
		if _filter!ID_CAT_ALL {
			ScanAllPlaylist ID_CAT_ALL

			dim numcatitem:dim catobjid:dim catlevid
			repeat numitem
				plitemid=cnt
				repeat 3
					switch _filter
						case ID_CAT_RANK_EX
							sdim tmpstr:tmpstr=GetScoreRank(double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)
							if tmpstr=="EX"|tmpstr=="EX+" {
								catobjid(numcatitem)=plitemid:catlevid(numcatitem)=cnt
								numcatitem++
							}
						swbreak
						case ID_CAT_RANK_S
							sdim tmpstr:tmpstr=GetScoreRank(double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)
							if tmpstr=="SSS"|tmpstr=="SS"|tmpstr=="S" {
								catobjid(numcatitem)=plitemid:catlevid(numcatitem)=cnt
								numcatitem++
							}
						swbreak
						case ID_CAT_RANK_AAA
							sdim tmpstr:tmpstr=GetScoreRank(double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)
							if tmpstr=="AAA"|tmpstr=="AAA+" {
								catobjid(numcatitem)=plitemid:catlevid(numcatitem)=cnt
								numcatitem++
							}
						swbreak
						case ID_CAT_RANK_AA
							sdim tmpstr:tmpstr=GetScoreRank(double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)
							if tmpstr=="AA"|tmpstr=="AA+" {
								catobjid(numcatitem)=plitemid:catlevid(numcatitem)=cnt
								numcatitem++
							}
						swbreak
						case ID_CAT_RANK_A
							sdim tmpstr:tmpstr=GetScoreRank(double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)
							if tmpstr=="A"|tmpstr=="A+" {
								catobjid(numcatitem)=plitemid:catlevid(numcatitem)=cnt
								numcatitem++
							}
						swbreak
					swend
				loop
			loop

			sdim plitem_Title@_org_,64,numitem,3
			sdim plitem_Artist@_org_,64,numitem,3
			sdim plitem_Source@_org_,64,numitem,3
			sdim plitem_UUID@_org_,32,numitem,3
			sdim plitem_Path@_org_,64,numitem,3
			dim plitem_mybest_score@_org_,numitem,3
			sdim plitem_mybest_rank@_org_,4,numitem,3
			sdim plitem_mybest_rate@_org_,5,numitem,3
			sdim plitem_mybest_cleartype@_org_,16,numitem,3
			dim plitem_mybest_judge@_org_,numitem,3,7
			sdim plitem_mybest_ratio@_org_,8,numitem,3
			sdim plitem_mybest_gauge@_org_,350,numitem,3
			repeat numitem
				plitemid=cnt
				repeat 3
					plitem_Title@_org_(plitemid,cnt)=plitem_Title(plitemid,cnt)
					plitem_Artist@_org_(plitemid,cnt)=plitem_Artist(plitemid,cnt)
					plitem_Source@_org_(plitemid,cnt)=plitem_Source(plitemid,cnt)
					plitem_UUID@_org_(plitemid,cnt)=plitem_UUID(plitemid,cnt)
					plitem_Path@_org_(plitemid,cnt)=plitem_Path(plitemid,cnt)
					plitem_mybest_score@_org_(plitemid,cnt)=plitem_mybest_score(plitemid,cnt)
					plitem_mybest_rank@_org_(plitemid,cnt)=plitem_mybest_rank(plitemid,cnt)
					plitem_mybest_rate@_org_(plitemid,cnt)=plitem_mybest_rate(plitemid,cnt)
					plitem_mybest_cleartype@_org_(plitemid,cnt)=plitem_mybest_cleartype(plitemid,cnt)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_EXCELLENT)=plitem_mybest_judge(plitemid,cnt,ID_DATA_EXCELLENT)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_GREAT)=plitem_mybest_judge(plitemid,cnt,ID_DATA_GREAT)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_GOOD)=plitem_mybest_judge(plitemid,cnt,ID_DATA_GOOD)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_BAD)=plitem_mybest_judge(plitemid,cnt,ID_DATA_BAD)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_MISS)=plitem_mybest_judge(plitemid,cnt,ID_DATA_MISS)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_EARLY)=plitem_mybest_judge(plitemid,cnt,ID_DATA_EARLY)
					plitem_mybest_judge@_org_(plitemid,cnt,ID_DATA_LATE)=plitem_mybest_judge(plitemid,cnt,ID_DATA_LATE)
					plitem_mybest_ratio@_org_(plitemid,cnt)=plitem_mybest_ratio(plitemid,cnt)
					plitem_mybest_gauge@_org_(plitemid,cnt)=plitem_mybest_gauge(plitemid,cnt)
				loop
			loop

			sdim plitem_Title,64,numcatitem,1
			sdim plitem_Artist,64,numcatitem,1
			sdim plitem_Source,64,numcatitem,1
			sdim plitem_UUID,32,numcatitem,1
			sdim plitem_Path,64,numcatitem,1
			dim plitem_mybest_score,numcatitem,1
			sdim plitem_mybest_rank,4,numcatitem,1
			sdim plitem_mybest_rate,5,numcatitem,1
			sdim plitem_mybest_cleartype,16,numcatitem,1
			dim plitem_mybest_judge,numcatitem,1,7
			sdim plitem_mybest_ratio,8,numcatitem,1
			sdim plitem_mybest_gauge,350,numcatitem,1

			repeat numcatitem
				plitem_Title(cnt,0)=plitem_Title@_org_(catobjid(cnt),catlevid(cnt))
				plitem_Artist(cnt,0)=plitem_Artist@_org_(catobjid(cnt),catlevid(cnt))
				plitem_Source(cnt,0)=plitem_Source@_org_(catobjid(cnt),catlevid(cnt))
				plitem_UUID(cnt,0)=plitem_UUID@_org_(catobjid(cnt),catlevid(cnt))
				plitem_Path(cnt,0)=plitem_Path@_org_(catobjid(cnt),catlevid(cnt))
				plitem_mybest_score(cnt,0)=plitem_mybest_score@_org_(catobjid(cnt),catlevid(cnt))
				plitem_mybest_rank(cnt,0)=plitem_mybest_rank@_org_(catobjid(cnt),catlevid(cnt))
				plitem_mybest_rate(cnt,0)=plitem_mybest_rate@_org_(catobjid(cnt),catlevid(cnt))
				plitem_mybest_cleartype(cnt,0)=plitem_mybest_cleartype@_org_(catobjid(cnt),catlevid(cnt))
				plitem_mybest_judge(cnt,0,ID_DATA_EXCELLENT)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_EXCELLENT)
				plitem_mybest_judge(cnt,0,ID_DATA_GREAT)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_GREAT)
				plitem_mybest_judge(cnt,0,ID_DATA_GOOD)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_GOOD)
				plitem_mybest_judge(cnt,0,ID_DATA_BAD)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_BAD)
				plitem_mybest_judge(cnt,0,ID_DATA_MISS)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_MISS)
				plitem_mybest_judge(cnt,0,ID_DATA_EARLY)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_EARLY)
				plitem_mybest_judge(cnt,0,ID_DATA_LATE)=plitem_mybest_judge@_org_(catobjid(cnt),catlevid(cnt),ID_DATA_LATE)
				plitem_mybest_ratio(cnt,0)=plitem_mybest_ratio@_org_(catobjid(cnt),catlevid(cnt))
				plitem_mybest_gauge(cnt,0)=plitem_mybest_gauge@_org_(catobjid(cnt),catlevid(cnt))
			loop
			numitem=numcatitem
		} else {
			dim numitem:sdim itemuuid

			numitem=int(GetINIvalue(coreBuf@_load_,"Playlist","Reg.Max"))	//項目数

			//変数初期化
			sdim plitem_Title,64,numitem,3
			sdim plitem_Artist,64,numitem,3
			sdim plitem_Source,64,numitem,3
			sdim plitem_UUID,32,numitem,3
			sdim plitem_Path,64,numitem,3
			dim plitem_mybest_score,numitem,3
			sdim plitem_mybest_rank,4,numitem,3
			sdim plitem_mybest_rate,5,numitem,3
			sdim plitem_mybest_cleartype,16,numitem,3
			dim plitem_mybest_judge,numitem,3,7
			sdim plitem_mybest_ratio,8,numitem,3
			sdim plitem_mybest_gauge,350,numitem,3

			//解析
			repeat numitem
				sdim tmpstr:tmpstr=GetINIvalue(coreBuf@_load_,"Playlist","ID."+cnt)
				split tmpstr,",",plitem_UUID(cnt,0),plitem_UUID(cnt,1),plitem_UUID(cnt,2)
				dim plitemid:plitemid=cnt
				repeat 3
					plitem_Path(plitemid,cnt)="Data/"+plitem_UUID(plitemid,cnt)+"/"+cnt+".bin"
					//譜面ファイルの存在を確認
					exist plitem_Path(plitemid,cnt)
					if strsize!-1 {
						//メタデータ取得
						sdim tmpstr,strsize:bload plitem_Path(plitemid,cnt),tmpstr
						plitem_Title(plitemid,cnt)=GetINIvalue(tmpstr,"Metadata","Title")
						plitem_Artist(plitemid,cnt)=GetINIvalue(tmpstr,"Metadata","Artist")
						plitem_Source(plitemid,cnt)=GetINIvalue(tmpstr,"Metadata","Source")

						//IR取得
						sdim IRsyncstruct
						WebAPI_SyncRecord IRsyncbuf,plitem_UUID(plitemid,cnt),IRsyncstruct
						plitem_mybest_score(plitemid,cnt)=int(IRsyncstruct(ID_REC_SCORE))
						plitem_mybest_rank(plitemid,cnt)=GetScoreRank(double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)
						plitem_mybest_rate(plitemid,cnt)=strf("%.1f",double(plitem_mybest_score(plitemid,cnt))/10000000.0*100.0)+"%"
						plitem_mybest_cleartype(plitemid,cnt)=IRsyncstruct(ID_REC_CLEARTYPE)
						plitem_mybest_judge(plitemid,cnt,ID_DATA_EXCELLENT)=int(IRsyncstruct(ID_REC_EXCELLENT))
						plitem_mybest_judge(plitemid,cnt,ID_DATA_GREAT)=int(IRsyncstruct(ID_REC_GREAT))
						plitem_mybest_judge(plitemid,cnt,ID_DATA_GOOD)=int(IRsyncstruct(ID_REC_GOOD))
						plitem_mybest_judge(plitemid,cnt,ID_DATA_BAD)=int(IRsyncstruct(ID_REC_BAD))
						plitem_mybest_judge(plitemid,cnt,ID_DATA_MISS)=int(IRsyncstruct(ID_REC_MISS))
						plitem_mybest_judge(plitemid,cnt,ID_DATA_EARLY)=int(IRsyncstruct(ID_REC_EARLY))
						plitem_mybest_judge(plitemid,cnt,ID_DATA_LATE)=int(IRsyncstruct(ID_REC_LATE))
						dim tmpint:tmpint=limit(int(IRsyncstruct(ID_REC_EARLY))+int(IRsyncstruct(ID_REC_LATE)),1)
						plitem_mybest_ratio(plitemid,cnt)=strf("%.1f",double(IRsyncstruct(ID_REC_EARLY))/double(tmpint))+"% / "
						plitem_mybest_ratio(plitemid,cnt)+strf("%.1f",double(IRsyncstruct(ID_REC_LATE))/double(tmpint))+"%"
						if IRsyncstruct(ID_REC_PACE_4BINHEX)=="" {
							poke plitem_mybest_gauge(plitemid,cnt),0,255
						} else {
							sdim HEXdecbin
							ConvHEXdec HEXdecbin,IRsyncstruct(ID_REC_PACE_4BINHEX)
							plitem_mybest_gauge(plitemid,cnt)=HEXdecbin
						}
					}
				loop
			loop
		}

		ActiveColumnID(ID_SONGS)=0
		bfActiveColumnID(ID_SONGS)=0
	return

	#deffunc StartMusicSelect
		//BGM再生
			PlaySE int(hbasschannel(bgmMain)),0.7

		//背景動画再生
			PlayMovieToGraph hdximg(ibg),DX_PLAYTYPE_LOOP

		//変数初期化
			dim RawWheelVal
			dim WheelVal,2
			dim ScrollEaseA,2
			dim ScrollEaseB,2
			dim OnScrollCount,2
			dim RawWheelVal
			dim bfRawWheelVal
			dim RawWheelMove
			dim bfHoverColumnID,2
			dim bfActiveColumnID,2
			dim HoverColumnID,2
			dim ActiveColumnID,2
			dim bfMouseInput
			dim CurSelLev
			dim PushButtonFlag
			dim DecideCount
			dim SceneCount
			DecideCount=-1
	return

	#deffunc DrawMusicSelect

		//カーソル位置補正
			dim CursorPosX:dim CursorPosY
			CursorPosX=int(double(CursorPosX@)*(double(BufWidth@)/double(DispWidth@)))
			CursorPosY=int(double(CursorPosY@)*(double(BufHeight@)/double(DispHeight@)))
			RawWheelVal=GetMouseWheelRotVol()
			RawWheelMove=0
			if RawWheelVal<0&bfRawWheelVal<0&RawWheelVal!bfRawWheelVal :RawWheelMove=RawWheelVal-bfRawWheelVal
			if RawWheelVal>0&bfRawWheelVal>0&RawWheelVal!bfRawWheelVal :RawWheelMove=RawWheelVal-bfRawWheelVal
			if (RawWheelVal>0&bfRawWheelVal<0)|(RawWheelVal<0&bfRawWheelVal>0) :RawWheelMove=RawWheelVal
			bfRawWheelVal=RawWheelVal
			PushButtonFlag=FALSE

		//カーソル位置から項目IDを取得
			HoverColumnID(ID_CATEGORY)=-1
			if insquare(CursorPosX,CursorPosY,0,229,450,229+811) {
				//カテゴリー選択
				repeat NUM_ITEM_CATEGORY
					dim SenseColumnPos
					SenseColumnPos=229+10+cnt*(50+8)+ScrollEaseB(ID_CATEGORY)
					if insquare(CursorPosX,CursorPosY,25,SenseColumnPos,25+640,SenseColumnPos+50) {
						HoverColumnID(ID_CATEGORY)=cnt
						break
					}
				loop
				bfHoverColumnID(ID_CATEGORY)=HoverColumnID(ID_CATEGORY)
				if ((GetMouseInput()&MOUSE_INPUT_LEFT)==1)&((bfMouseInput&MOUSE_INPUT_LEFT)==0)&HoverColumnID(ID_CATEGORY)!-1 {
					ActiveColumnID(ID_CATEGORY)=HoverColumnID(ID_CATEGORY)
					ScanAllPlaylist ActiveColumnID(ID_CATEGORY)
					PushButtonFlag=TRUE
				}
			}
			HoverColumnID(ID_SONGS)=-1
			if insquare(CursorPosX,CursorPosY,450,229,450+700,229+811) {
				//曲選択
				repeat numitem
					dim SenseColumnPos
					SenseColumnPos=229+10+cnt*(50+8)+ScrollEaseB(ID_SONGS)
					if insquare(CursorPosX,CursorPosY,450,SenseColumnPos,450+700,SenseColumnPos+50) {
						HoverColumnID(ID_SONGS)=cnt
						break
					}
				loop
				bfHoverColumnID(ID_SONGS)=HoverColumnID(ID_SONGS)
				if ((GetMouseInput()&MOUSE_INPUT_LEFT)==1)&((bfMouseInput&MOUSE_INPUT_LEFT)==0)&HoverColumnID(ID_SONGS)!-1 {
					ActiveColumnID(ID_SONGS)=HoverColumnID(ID_SONGS)
					PushButtonFlag=TRUE
				}
			}

		//背景動画
			SetDrawBlendMode DX_BLENDMODE_NOBLEND,256
			DrawExtendGraph 0,0,BufWidth@,BufHeight@,hdximg(ibg),FALSE
			GraphFilterRectBltS hdximg@(iScene),hdximg@(iScene),0,0,BufWidth@,BufHeight@,0,0,DX_GRAPH_FILTER_GAUSS,32,800

		//----以下、シーン別レイヤー-----
		SetDrawScreen hdximg(ilayer_all)
		ClearDrawScreen

		//ナビゲーション
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 48,32,hdximg(inav),TRUE

		//ウィンドウの背景
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 0,165,hdximg(ibg_window),TRUE

		//カテゴリー選択ウィンドウ
			SetDrawScreen hdximg(iwindow_category)
			ClearDrawScreen
				if insquare(CursorPosX,CursorPosY,0,229,450,229+811) {
					WheelVal(ID_CATEGORY)=RawWheelMove
					RawWheelMove=0
				}
				if WheelVal(ID_CATEGORY)>0 { //＋
					ScrollEaseA(ID_CATEGORY)=ScrollEaseB(ID_CATEGORY)
					ScrollEaseB(ID_CATEGORY)+80
					OnScrollCount(ID_CATEGORY)=0
				}
				if WheelVal(ID_CATEGORY)<0 { //ー
					ScrollEaseA(ID_CATEGORY)=ScrollEaseB(ID_CATEGORY)
					ScrollEaseB(ID_CATEGORY)-80
					OnScrollCount(ID_CATEGORY)=0
				}
				if ScrollEaseB(ID_CATEGORY)>0 :ScrollEaseB(ID_CATEGORY)=0
				if (10*2+(8+50)*NUM_ITEM_CATEGORY)-811>0 {
					if ScrollEaseB(ID_CATEGORY)<-((10*2+(8+50)*NUM_ITEM_CATEGORY)-811) :ScrollEaseB(ID_CATEGORY)=-((10*2+(8+50)*14)-811)
				}
				AddCount OnScrollCount(ID_CATEGORY)
				//アイテム描画
				repeat NUM_ITEM_CATEGORY
					setease ScrollEaseA(ID_CATEGORY),ScrollEaseB(ID_CATEGORY),ease_cubic_out
					dim itemposy:itemposy=10+cnt*(50+8)+getease(OnScrollCount(ID_CATEGORY),10)
					//項目
						SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
						DrawGraph 25,itemposy,hdximg(ifolder_all+cnt),TRUE
					//ホバーエフェクト
						if HoverColumnID(ID_CATEGORY)==cnt {
							setease 0,100,ease_linear+ease_loop
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,20)
							DrawGraph 21,itemposy-4,hdximg(icursor_category),TRUE
						}
					//アクティブエフェクト
						if ActiveColumnID(ID_CATEGORY)==cnt {
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
							DrawGraph 21,itemposy-4,hdximg(icursor_category),TRUE
						}
				loop
			SetDrawScreen hdximg(ilayer_all)
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 0,229,hdximg(iwindow_category),TRUE

		//曲選択ウィンドウ
			SetDrawScreen hdximg(iwindow_songs)
			ClearDrawScreen
				if insquare(CursorPosX,CursorPosY,450,229,450+700,229+811) {
					WheelVal(ID_SONGS)=RawWheelMove
					RawWheelMove=0
				}
				if WheelVal(ID_SONGS)>0 { //＋
					ScrollEaseA(ID_SONGS)=ScrollEaseB(ID_SONGS)
					ScrollEaseB(ID_SONGS)+80
					OnScrollCount(ID_SONGS)=0
				}
				if WheelVal(ID_SONGS)<0 { //ー
					ScrollEaseA(ID_SONGS)=ScrollEaseB(ID_SONGS)
					ScrollEaseB(ID_SONGS)-80
					OnScrollCount(ID_SONGS)=0
				}
				if ScrollEaseB(ID_SONGS)>0 :ScrollEaseB(ID_SONGS)=0
				if (10*2+(8+50)*numitem)-811>0 {
					if ScrollEaseB(ID_SONGS)<-((10*2+(8+50)*numitem)-811) :ScrollEaseB(ID_SONGS)=-((10*2+(8+50)*numitem)-811)
				} else :ScrollEaseB(ID_SONGS)=0
				AddCount OnScrollCount(ID_SONGS)
				//アイテム描画
				repeat numitem
					setease ScrollEaseA(ID_SONGS),ScrollEaseB(ID_SONGS),ease_cubic_out
					dim itemposy:itemposy=10+cnt*(50+8)+getease(OnScrollCount(ID_SONGS),10)
					if itemposy<-50 :continue
					if itemposy>BufHeight@:continue
					//項目
						SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
						DrawGraph 15,itemposy,hdximg(ibg_item),TRUE
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 47,itemposy+11,plitem_Title(cnt,0),GetColor(10,10,10),hdxfont(fNSCJM24)

					//ホバーエフェクト
						if HoverColumnID(ID_SONGS)==cnt {
							setease 0,100,ease_linear+ease_loop
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,20)
							DrawGraph 11,itemposy-4,hdximg(icursor_list),TRUE
						}
					//アクティブエフェクト
						if ActiveColumnID(ID_SONGS)==cnt {
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
							DrawGraph 11,itemposy-4,hdximg(icursor_list),TRUE
						}
				loop
			SetDrawScreen hdximg(ilayer_all)
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 450,229,hdximg(iwindow_songs),TRUE

		//メタデータ
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 1170,189,hdximg(ibg_meta),TRUE
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			//曲名
			DrawStringToHandle 1492,231,plitem_Title(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(10,10,10),hdxfont(fNSCJM24)
			//アーティスト名
			DrawStringToHandle 1492,283,plitem_Artist(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(30,30,30),hdxfont(fNSCJM20)
			//アルバム
			DrawStringToHandle 1492,333,plitem_Source(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(50,50,50),hdxfont(fNSCJM16)

		//詳細
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 1150,425,hdximg(ibg_detail),TRUE
			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			DrawStringToHandle 1173,536,strf("%08d",plitem_mybest_score(ActiveColumnID(ID_SONGS),CurSelLev)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1273,536,plitem_mybest_rank(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1333,536,plitem_mybest_rate(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1398,536,plitem_mybest_cleartype(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1269,585,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_EXCELLENT)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1269,604,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_GREAT)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1269,623,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_GOOD)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1269,642,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_BAD)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1269,661,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_MISS)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1398,585,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_EARLY)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1398,604,strf("%04d",plitem_mybest_judge(ActiveColumnID(ID_SONGS),CurSelLev,ID_DATA_LATE)),GetColor(255,255,255),hdxfont(fPS16)
			DrawStringToHandle 1398,625,plitem_mybest_ratio(ActiveColumnID(ID_SONGS),CurSelLev),GetColor(255,255,255),hdxfont(fPS16)
			if peek(plitem_mybest_gauge(ActiveColumnID(ID_SONGS),CurSelLev),0)==255 {		//ゲージ記録なし
				setease 100,256,ease_linear+ease_loop
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneCount,60)
				DrawGraph 1170,706,hdximg(ilayer_nodata),TRUE
			} else {
				repeat 350-1		//グラフ描画
					dim GraphColorCode:dim GraphDrawValue
					GraphDrawValue(0)=int(double(peek(plitem_mybest_gauge(ActiveColumnID(ID_SONGS),CurSelLev),cnt))*0.8)
					GraphDrawValue(1)=int(double(peek(plitem_mybest_gauge(ActiveColumnID(ID_SONGS),CurSelLev),cnt+1))*0.8)

					GraphColorCode=GetColor(255,255,255)
					if plitem_mybest_cleartype(ActiveColumnID(ID_SONGS),CurSelLev)=="ASSIST-CLEAR"|plitem_mybest_cleartype(ActiveColumnID(ID_SONGS),CurSelLev)=="CLEAR" {		//加点式
						if GraphDrawValue(0)>70.0 :GraphColorCode=GetColor(100,255,100):else:GraphColorCode=GetColor(255,50,50)
					}
					if plitem_mybest_cleartype(ActiveColumnID(ID_SONGS),CurSelLev)=="EXPERT-CLEAR" :GraphColorCode=GetColor(255,50,80)	//EXPERTゲージ
					if plitem_mybest_cleartype(ActiveColumnID(ID_SONGS),CurSelLev)=="MADNESS-CLEAR" :GraphColorCode=GetColor(132,60,160) //MADNESSゲージ

					DrawLine 1170+cnt,706+80-GraphDrawValue(0),1170+1+cnt,706+80-GraphDrawValue(1),GraphColorCode,2
				loop
			}

		//プレイ開始ボタン
			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			if insquare(CursorPosX,CursorPosY,1620,960,1620+280,960+64) {
				if (GetMouseInput()&MOUSE_INPUT_LEFT)&DecideCount==-1 {
					DrawGraph 1620,960,hdximg(ibutton_start_2),TRUE
					PushButtonFlag=TRUE
					PlaySE int(hbasschannel(sndDecide)),1.0
					DecideCount=0
				} else {
					DrawGraph 1620,960,hdximg(ibutton_start_1),TRUE
				}
			} else {
				DrawGraph 1620,960,hdximg(ibutton_start_0),TRUE
			}

		//難易度変更ボタン
			if ActiveColumnID(ID_CATEGORY)==0 {	//すべての楽曲のときのみ
				if plitem_UUID(ActiveColumnID(ID_SONGS),0)!"*" {
					if CurSelLev==0 {	//normal
						DrawGraph 1254,826,hdximg(ibutton_normal_2),TRUE
					} else {
						if insquare(CursorPosX,CursorPosY,1254,826,1254+150,826+32) {
							if GetMouseInput()&MOUSE_INPUT_LEFT :CurSelLev=0:PushButtonFlag=TRUE
							DrawGraph 1254,826,hdximg(ibutton_normal_1),TRUE
						} else {
							DrawGraph 1254,826,hdximg(ibutton_normal_0),TRUE
						}
					}
				}
				if plitem_UUID(ActiveColumnID(ID_SONGS),1)!"*" {
					if CurSelLev==1 {	//hard
						DrawGraph 1254,866,hdximg(ibutton_hard_2),TRUE
					} else {
						if insquare(CursorPosX,CursorPosY,1254,866,1254+150,866+32) {
							if GetMouseInput()&MOUSE_INPUT_LEFT :CurSelLev=1:PushButtonFlag=TRUE
							DrawGraph 1254,866,hdximg(ibutton_hard_1),TRUE
						} else {
							DrawGraph 1254,866,hdximg(ibutton_hard_0),TRUE
						}
					}
				}
				if plitem_UUID(ActiveColumnID(ID_SONGS),2)!"*" {
					if CurSelLev==2 {	//chaos
						DrawGraph 1254,906,hdximg(ibutton_chaos_2),TRUE
					} else {
						if insquare(CursorPosX,CursorPosY,1254,906,1254+150,906+32) {
							if GetMouseInput()&MOUSE_INPUT_LEFT :CurSelLev=2:PushButtonFlag=TRUE
							DrawGraph 1254,906,hdximg(ibutton_chaos_1),TRUE
						} else {
							DrawGraph 1254,906,hdximg(ibutton_chaos_0),TRUE
						}
					}
				}
			}

		//ステータス
			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			DrawBox 0,BufHeight@-40,BufWidth@,BufHeight@,$272727,TRUE
			DrawStringToHandle 24,1048,"XODA SAPPHIRE",GetColor(230,230,230),hdxfont(fPS20)
			DrawStringToHandle 220,1052,"Music Select",GetColor(210,210,210),hdxfont(fPS14)

		if PushButtonFlag==TRUE&(GetMouseInput()&MOUSE_INPUT_LEFT)==TRUE&(bfMouseInput&MOUSE_INPUT_LEFT)==FALSE {
			PlaySE int(hbasschannel(sndCursor)),1.0
		}

		//-----以下、共有レイヤー-----
		SetDrawScreen hdximg@(iScene)
		setease 1,1.2,ease_cubic_out
		dim EasePosX:dim EasePosY:dim EaseWidth:dim EaseHeight
		EaseWidth=int(double(BufWidth@)*geteasef(DecideCount+1,20))
		EaseHeight=int(double(BufHeight@)*geteasef(DecideCount+1,20))
		EasePosX=BufWidth@/2-EaseWidth/2:EasePosY=BufHeight@/2-EaseHeight/2
		setease 256,0,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(DecideCount+1,20)
		DrawExtendGraph EasePosX,EasePosY,EasePosX+EaseWidth,EasePosY+EaseHeight,hdximg(ilayer_all),TRUE

		setease 0,256,ease_linear
		SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(DecideCount-25,TransitionFrame)
		DrawBox 0,0,BufWidth@,BufHeight@,GetColor(255,255,255),TRUE

		setease 256,0,ease_linear
		SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount,TransitionFrame)
		DrawBox 0,0,BufWidth@,BufHeight@,GetColor(0,0,0),TRUE

		if DecideCount>25+TransitionFrame {
			//---debug---
			DrawLoadTransition $FFFFFF,TransitionFrame,0
			InitScene ID_PLAY
			LoadGamePlayImages
			InitGamePlayFonts
			LoadPartnerIcon
			OpenChartFile"Data/"+plitem_UUID(ActiveColumnID(ID_SONGS),CurSelLev)+"/"+CurSelLev+".bin"
			cfunc64v BASS_ChannelStop,hbasschannel(bgmMain)
			DrawLoadTransition 0,10,1
			SetDrawScreen hdximg@(iScene)
			ClearDrawScreen
			wait 50
			StartGameplay
		}

		AddCount SceneCount
		bfMouseInput=GetMouseInput()
		if DecideCount!-1 :AddCount DecideCount

	return
#global