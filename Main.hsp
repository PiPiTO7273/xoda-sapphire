
#include"hsp3_64.as"
#include"dxlib_x64.as"
#include"a2d.hsp"
#include"bass_x64.as"
#include"hspint64.as"

#include"NameSpace.hsp"
#include"mod_BinPkg.hsp"
#include"mod_dxlibplus.hsp"
#include"mod_4bithex.hsp"
#include"mod_wininet64.hsp"
#include"func_Default.hsp"
#include"func_Load.hsp"
#include"func_Sync.hsp"
#include"func_Background.hsp"
#include"svc_Layer.hsp"
#include"svc_Online.hsp"
#include"scene_Launcher.hsp"
#include"scene_Title.hsp"
#include"scene_Lobby.hsp"
#include"scene_MusicSelect.hsp"
#include"scene_Play.hsp"
#include"scene_Result.hsp"
#include"scene_KeyConfig.hsp"
#include"scene_PartnerSelect.hsp"
#include"scene_Challenge.hsp"
#include"scene_Setup.hsp"

#packopt hide 1

	gsel 0,-1

	LoadCoreSettings"core.ini"
	if GetINIvalue(coreBuf@_load_,"Hash","core")!hash_core {
		dialog"err"
		goto*exit
	}
	InitDxlib
	InitBASSlib
	LoadCoreImages
	InitCoreSceneMan
	randomize

	DrawLoadScreen

	//セーブデータ同期
		SetDefaultSaveData
		WebAPI_Init geturl_server(httphost@),httpproxy@
		sdim LoginUserInfo
		LoginUserInfo(ID_HOST)=httphost@
		strrep LoginUserInfo(ID_HOST),"http://","":strrep LoginUserInfo(ID_HOST),"https://",""
		LoginUserInfo(ID_NAME)=GetINIvalue(coreBuf@_load_,"Auth","Name")
		LoginUserInfo(ID_KEY)=GetINIvalue(coreBuf@_load_,"Auth","Password")
		LoginUserInfo(ID_DECODE)=GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_0")
		LoginUserInfo(ID_DECODE)+GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_1")
		LoginUserInfo(ID_DECODE)+GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_2")
		LoginUserInfo(ID_DECODE)+GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_3")
		RegLoginInfo LoginUserInfo

	;CheckNetworkConnection

	DrawLoadScreen

	LoadTitleImages
	LoadErrorImages
	LoadSigninImages

	LoadTitleFonts
	InitScene ID_TITLE
	StartTitle

*MainLoop

	ProcessMessage
	if stat==-1 :goto*Exit
	await	//HSPプロセス自体のウィンドウメッセージ処理

	//垂直同期が無効の場合のFPS補正
		ddim curFPS@,1
		curFPS@=double(cfunc64(RET_FLOAT,GetFPS))

	//キー判定
	repeat 256:KeyFlag(cnt)=CheckHitKey(cnt):loop

	SetDrawScreen hdximg@(iScene)
	ClearDrawScreen
	SetDrawMode DX_DRAWMODE_BILINEAR

	switch curSceneID
		case ID_TITLE:DrawTitle:swbreak
		case ID_PLAY:DrawGamePlay:swbreak
		case ID_LOBBY:DrawLobby:swbreak
		case ID_MUSICSELECT:DrawMusicSelect:swbreak
		case ID_KEYCONFIG:DrawKeyConfig:swbreak
		case ID_RESULT:DrawResult:swbreak
		case ID_PARTNERSELECT:DrawPartnerSelect:swbreak
		case ID_CHALLENGE:DrawChallenge:swbreak
		case ID_FATALERROR:DrawError:swbreak
		case ID_SIGNIN:DrawSignin:swbreak
		case ID_REGISTER:DrawRegister:swbreak
		case ID_WELCOME0:DrawWelcome 0:swbreak
		case ID_WELCOME1:DrawWelcome 1:swbreak
		case ID_WELCOME2:DrawWelcome 2:swbreak
		case ID_DUMMY:DrawDummy:swbreak
	swend

	SetDrawScreen DX_SCREEN_BACK
	ClearDrawScreen

	//シーン
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawExtendGraph 0,0,DispWidth@,DispHeight@,hdximg@(iScene),TRUE

	//マウスカーソル
		cfunc64v GetMousePoint,varptr64(CursorPosX@),varptr64(CursorPosY@)
		if DrawCursorFlag@==TRUE|FullScreenFlag@==TRUE {
			if CursorHideCount@>FPSvalue@*2&curSceneID==ID_PLAY {
				setease 256,0,ease_linear
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(CursorHideCount@-FPSvalue@*2,FPSvalue@/4)
			} else {
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			}
			DrawGraph CursorPosX@-32,CursorPosY@-32,hdximg_arr_cursor@(CursorImgCount@),TRUE
		}

		if bfCursorPosX@!CursorPosX@|bfCursorPosY@!CursorPosY@ {
			CursorHideCount@=0
		}
		bfCursorPosX@=CursorPosX@:bfCursorPosY@=CursorPosY@

	//デバッグ文字列
	if DebugStringFlag@ {
		ChangeFont"MS GOTHIC":SetFontSize 16
		ChangeFontType DX_FONTTYPE_NORMAL
		DrawDebugString 0,0,"Sapphire @debug"
		DrawDebugString 0,1,"HD "+BufWidth@+"x"+BufHeight@+" -> "+DispWidth@+"x"+DispHeight@
		DrawDebugString 0,2,"Server : "+LoginUserInfo(ID_HOST)+" @\""+LoginUserInfo(ID_NAME)+"\" & \""+LoginUserInfo(ID_KEY)+"\""
		DrawDebugString 0,4,"FPS="+double(curFPS@)
		DrawDebugString 0,6,"C="+coreCount+" F="+FrameCount+" S="+SceneCount+" ,Level="+CounterLev@
		DrawDebugString 0,7,"Cursor ("+CursorPosX+","+CursorPosY+") "+GetMouseWheelRotVol()
	}

	//開発者用コマンド
	if KeyFlag(KEY_INPUT_F11)==TRUE {
		OpenCommandMenu
	}

	//エラー検出
	if FatalErrorFlag@==TRUE {
		curSceneID=ID_FATALERROR
	}

	coreCount++
	AddCount FrameCount@
	AddCount SceneCount@
	repeat 256:bfKeyFlag(cnt)=KeyFlag(cnt):loop
	AddCount CursorImgCount@
	AddCount CursorHideCount@
	if CursorImgCount@>57 :CursorImgCount@=0

	ScreenFlip

	goto*MainLoop

*ExitProg
	dxlib_End
	BASS_Free
	closeHttp
	end
*Exit
	SendAllProfile
	goto*ExitProg