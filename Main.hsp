
// Sapphire x64
// (c) 2020-2022 puoro. / puoro. Games

#include"hsp3_64.as"					//HSP3 x64 ランタイム
#include"dxlib_x64.as"					//DXライブラリモジュール
#include"a2d.hsp"						//Artlet2Dモジュール
#include"bass_x64.as"					//Bass Audio Libraryモジュール
#include"hspint64.as"					//HSPint64 (by inovia)

#addition"AccKey.hsp"					//APIキー、復号化キーなど
#include"NameSpace.hsp"					//グローバル空間における定数
#include"mod_BinPkg.hsp"				//Binary Packageモジュール
#include"mod_dxlibplus.hsp"				//DXライブラリ拡張モジュール
#include"mod_4bithex.hsp"				//バイナリを文字列に書き換えるモジュール
#include"mod_wininet64.hsp"				//wininetモジュール
#include"func_Default.hsp"				//基本的な呼び出し命令・関数
#include"func_Load.hsp"					//ロード、初期化関連
#include"func_Sync.hsp"					//WebAPI、セーブデータ同期関連
#include"func_Background.hsp"			//背景、演出関連
#include"svc_Layer.hsp"					//システムレイヤー
#include"svc_Online.hsp"				//通信関連のレイヤー
#include"scene_Title.hsp"				//タイトル画面
#include"scene_MusicSelect.hsp"			//曲選択画面
#include"scene_Lobby.hsp"				//ロビー
#include"scene_Play.hsp"				//ゲームプレイ
#include"scene_Result.hsp"				//リザルト
#include"scene_KeyConfig.hsp"			//キー割り当て設定
#include"scene_PartnerSelect.hsp"		//パートナー選択
#include"scene_Challenge.hsp"			//チャレンジモード
#include"scene_Setup.hsp"				//初期設定、チュートリアル
#include"scene_Library.hsp"				//ライブラリ
#include"scene_Shop.hsp"				//ショップ

#packopt hide 1

	gsel 0,-1

	//ランチャーアップデートを検知
	exist"Data/_cache/update_SapphireLauncher.exe"
	if strsize!-1 {
		screen 0,480,120
		title"puoro. updater"
		font"MS GOTHIC",16,1
		pos 20,40:mes"しばらくお待ち下さい・・・"
		wait 100
		bcopy"Data/_cache/update_SapphireLauncher.exe","SapphireLauncher.exe"
		wait 100
		delete"Data/_cache/update_SapphireLauncher.exe"
		wait 100
		exist"SapphireLauncher.exe"
		if strsize!-1 {
			wait 100
			exec"SapphireLauncher.exe"
		}
		repeat
			end
		loop
	}

	LoadCoreSettings"core.ini"
	if GetINIvalue(coreBuf@_load_,"Hash","core")!hash_core {
		dialog"err"
		goto*exit
	}
	InitDxlib
	InitBASSlib
	LoadCoreImages
	InitCoreSceneMan
	randomize

	DrawLoadScreen

	//セーブデータ同期
		SetDefaultSaveData
		WebAPI_Init geturl_server(httphost@),httpproxy@
		sdim LoginUserInfo
		LoginUserInfo(ID_HOST)=httphost@
		strrep LoginUserInfo(ID_HOST),"http://","":strrep LoginUserInfo(ID_HOST),"https://",""
		LoginUserInfo(ID_NAME)=GetINIvalue(coreBuf@_load_,"Auth","Name")
		LoginUserInfo(ID_KEY)=GetINIvalue(coreBuf@_load_,"Auth","Password")
		LoginUserInfo(ID_DECODE)=GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_0")
		LoginUserInfo(ID_DECODE)+GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_1")
		LoginUserInfo(ID_DECODE)+GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_2")
		LoginUserInfo(ID_DECODE)+GetINIvalue(coreBuf@_load_,"PortalSvc","SSL_Decode_3")
		RegLoginInfo LoginUserInfo

	CheckNetworkConnection:DrawLoadScreen

	WebAPI_SyncProfile:DrawLoadScreen
	LoadMusicSelectImages:DrawLoadScreen
	LoadMusicSelectSounds:DrawLoadScreen
	LoadGamePlayImages:DrawLoadScreen
	LoadPartnerIcon:DrawLoadScreen
	InitGamePlaySounds:DrawLoadScreen
	LoadKeyConfigImages:DrawLoadScreen
	LoadResultImages:DrawLoadScreen
	LoadResultSounds:DrawLoadScreen
	LoadPartnerSelectImages:DrawLoadScreen
	LoadChallengeModeMap"Data/map-challenge.bin":DrawLoadScreen
	LoadChallengeImages:DrawLoadScreen
	LoadLibraryImages:DrawLoadScreen
	LoadShopImages:DrawLoadScreen
	LoadShopItems:DrawLoadScreen
	LoadTitleImages:DrawLoadScreen
	LoadTitleSounds:DrawLoadScreen
	LoadErrorImages:DrawLoadScreen
	LoadSigninImages:DrawLoadScreen

	LoadTitleFonts
	InitScene ID_TITLE
	StartTitle
*MainLoop

	ProcessMessage
	if stat==-1 :goto*Exit
	await	//HSPプロセス自体のウィンドウメッセージ処理

	//垂直同期が無効の場合のFPS補正
		ddim curFPS@,1
		curFPS@=double(cfunc64(RET_FLOAT,GetFPS))

	//キー判定
	repeat 256:KeyFlag(cnt)=CheckHitKey(cnt):loop

	SetDrawScreen hdximg@(iScene)
	ClearDrawScreen
	SetDrawMode DX_DRAWMODE_BILINEAR

	switch curSceneID
		case ID_TITLE:DrawTitle:swbreak
		case ID_PLAY:DrawGamePlay:swbreak
		case ID_LOBBY:DrawLobby:swbreak
		case ID_MUSICSELECT:DrawMusicSelect:swbreak
		case ID_KEYCONFIG:DrawKeyConfig:swbreak
		case ID_RESULT:DrawResult:swbreak
		case ID_PARTNERSELECT:DrawPartnerSelect:swbreak
		case ID_CHALLENGE:DrawChallenge:swbreak
		case ID_LIBRARY:DrawLibrary:swbreak
		case ID_SHOP:DrawShop:swbreak
		case ID_FATALERROR:DrawError:swbreak
		case ID_SIGNIN:DrawSignin:swbreak
		case ID_REGISTER:DrawRegister:swbreak
		case ID_WELCOME0:DrawWelcome 0:swbreak
		case ID_WELCOME1:DrawWelcome 1:swbreak
		case ID_WELCOME2:DrawWelcome 2:swbreak
		case ID_DUMMY:DrawDummy:swbreak
	swend

	SetDrawScreen DX_SCREEN_BACK
	ClearDrawScreen

	//シーン
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawExtendGraph 0,0,DispWidth@,DispHeight@,hdximg@(iScene),TRUE

	DrawSystemLayer

	coreCount++
	AddCount FrameCount@
	AddCount SceneCount@
	repeat 256:bfKeyFlag(cnt)=KeyFlag(cnt):loop
	AddCount CursorImgCount@
	AddCount CursorHideCount@
	if CursorImgCount@>57 :CursorImgCount@=0

	if SceneCount@\FPSvalue@==0 {
		librec_PlayTime@++
	}

	ScreenFlip

	goto*MainLoop

*Exit
	dxlib_End
	BASS_Free
	SendAllProfile
	closeHttp
	end