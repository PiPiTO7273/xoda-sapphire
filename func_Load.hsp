
#module _Load_

	//起動設定を読み込み
	#deffunc LoadCoreSettings str _filename
		exist _filename
		if strsize==-1 :return -1
		sdim coreBuf,strsize
		bload _filename,coreBuf

		dim DispWidth@
		DispWidth@=int(GetINIvalue(coreBuf,"Graphics","Display.Width"))
		dim DispHeight@
		DispHeight@=int(GetINIvalue(coreBuf,"Graphics","Display.Height"))
		dim BufWidth@
		BufWidth@=int(GetINIvalue(coreBuf,"Graphics","Buffer.Width"))
		dim BufHeight@
		BufHeight@=int(GetINIvalue(coreBuf,"Graphics","Buffer.Height"))
		dim FPSvalue@
		FPSvalue@=int(GetINIvalue(coreBuf,"Graphics","Refresh"))
		dim VsyncFlag@
		VsyncFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","Vsync"))
		dim FullScreenFlag@
		FullScreenFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","FullScreen"))
		dim BGAblurlev@
		sdim tmpstr:tmpstr=GetINIvalue(coreBuf,"Graphics","Movie.Blur")
		strrep tmpstr,"x",""
		BGAblurlev@=int(tmpstr)
		sdim BGAquality@
		switch GetINIvalue(coreBuf,"Graphics","Movie.Quality")
			case"high":BGAquality@="hq":swbreak
			case"mid":BGAquality@="mq":swbreak
			case"low":BGAquality@="lq":swbreak
			case"disable":BGAquality@="*":swbreak
		swend
		dim LowQuaFlag@
		LowQuaFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","Low.Quality"))
		dim CounterLev@
		CounterLev@=int(GetINIvalue(coreBuf,"Graphics","Counter.Level"))
		dim DrawCursorFlag@
		DrawCursorFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","Use.CustomCursor"))

	return

	//DXライブラリを初期化
	#deffunc InitDxlib
		SetOutApplicationLogValidFlag 0
		;SetUseDirectDrawDeviceIndex 0
		ChangeWindowMode FullScreenFlag@!1
		SetWindowStyleMode 7
		SetWindowSizeChangeEnableFlag TRUE,TRUE
		SetGraphMode DispWidth@,DispHeight@,32
		SetAlwaysRunFlag TRUE
		SetWaitVSyncFlag VsyncFlag@
		SetMainWindowText"XODA SAPPHIRE"
		dxlib_Init
		if stat==-1 {
			dialog"グラフィック機能の初期化に失敗しました。"
		}
		;SetUseGDIFlag TRUE

		//変数初期化
			dim KeyFlag@,256
			dim bfKeyFlag@,256
			dim CursorImgCount@
			dim CursorHideCount@
			dim bfCursorPosX@:dim bfCursorPosY@
			dim CursorPosX@:dim CursorPosY@
	return

	//BASS Audio Libraryを初期化
	#deffunc InitBASSlib
		BASS_Init -1,44100,0,hwnd,0
		if stat==0 {
			dialog"オーディオ機能の初期化に失敗しました。"
		}
		BASS_SetConfig BASS_CONFIG_BUFFER,32
		if stat==0 {
			dialog"オーディオバッファの設定に失敗しました。"
		}
		BASS_SetConfig BASS_CONFIG_DEV_BUFFER,16
		if stat==0 {
			dialog"オーディオバッファの設定に失敗しました。"
		}
	return

	//グローバル空間のグラフィックを作成
	#deffunc LoadCoreImages
		SetUsePremulAlphaConvertLoad TRUE
		SetDXArchiveExtension"bin"
		hdximg@(iScene)=MakeScreen(BufWidth@,BufHeight@,TRUE)
		dim hdximg_arr_cursor@,58
		repeat 58
			hdximg_arr_cursor@(cnt)=LoadGraph("Data/res_cursor/"+cnt+".png")
		loop
		hdximg@(ibg_load)=LoadGraph("Data/bg_Load.ogv")
		hdximg@(ibg_load_pic)=LoadGraph("Data/bg_Load.png")
		hdximg@(iprogbar)=LoadGraph("Data/progbar_load.png")
		hdximg@(ibg_tips)=LoadGraph("Data/bg_Tips.png")
		PlayMovieToGraph hdximg@(ibg_load),DX_PLAYTYPE_LOOP
	return

	//グローバル空間の変数を初期化
	#deffunc InitCoreSceneMan
		dim FrameCount@
		dim coreCount@
		dim bfWheelVal@
		dim WheelVal@
		dim TotalScrollVal@
		dim FPSawaitLoop@
	return

	//シーンを初期化
	#deffunc InitScene int _sceneid
		dim SceneCount@
		dim SceneHalfCount@
		dim curSceneID@
		curSceneID@=_sceneid
	return

	//ムービーのエイリアスからIDを生成
	#defcfunc GetMovieAlias str _alias
		sdim tmpalias_lib:dim resid
		tmpalias_lib(0)="hexa","wave","ring","sq3d","cub0","li3d","cve0","tri0","cyb0","cir0","synp","wav1","city","dna*","cyb1","wav2"
		tmpalias_lib(16)="cir1","tri1","tri2","mn3d","cub1","cyb2","cyb3","glas","wav3","cyb4","tri3","ball","cve1","lght","cbci","spce"
		tmpalias_lib(32)="cir2"
		resid=-1
		repeat length(tmpalias_lib)
			if _alias==tmpalias_lib(cnt) :resid=cnt:break
		loop
	return resid

	//パートナーのアイコンを読み込み
	#deffunc LoadPartnerIcon
		exist"Data/res_char.bin"
		if strsize==-1 {
			return -1
		}

		dim CharMax@charicon
		sdim CharImgPath@charicon
		dim hCharImg@charicon
		sdim CharName@charicon
		sdim CharDesc@charicon
		sdim CharSkill@charicon
		dim CharLev@charicon
		CharMax@charicon=int(GetINIvalue(coreBuf,"Partner","Reg.Max"))*2
		SetDXArchiveExtension"bin"
		repeat CharMax@charicon/2
			CharImgPath@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Path")
			hCharImg@charicon(cnt*2)=LoadGraph("Data/"+CharImgPath@charicon(cnt*2))
			CharName@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Name")
			CharDesc@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Desc")
			CharSkill@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Skill")
			CharLev@charicon(cnt*2)=1
			CharImgPath@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Path")
			hCharImg@charicon(cnt*2+1)=LoadGraph("Data/"+CharImgPath@charicon(cnt*2+1))
			CharName@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Name")
			CharDesc@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Desc")
			CharSkill@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Skill")
			CharLev@charicon(cnt*2+1)=1
		loop
	return

	#define global DrawLoadScreen(%1="",%2=0,%3=256,%4=0) _DrawLoadScreen %1,%2,%3,%4
	#deffunc _DrawLoadScreen str _msg,int _progval,int _alpha,int _color

		sdim tipstxt,10
		tipstxt(0)="カルディのドライマンゴーはマジで美味しいから。\nみんな買ったほうがいいよ。"
		tipstxt(1)="マックの期間限定とかいろいろあるけど、\n結局はソーセージマフィンが一番安いし美味い。"
		tipstxt(2)="ダイエット中なのに隣で夜食食うやつってさ\nマジで腹立つよね"
		tipstxt(3)="ロード画面って暇よな"
		tipstxt(4)="烏龍茶飲んだら脂肪の吸収が抑えられるとか聞くけど\n実際のところどうなんだろうね"
		tipstxt(5)="和式のトイレ嫌い"
		tipstxt(6)="このTipsって意味ある？"
		tipstxt(7)="ペットボトルの飲み物って中途半端に残っちゃうよね"
		tipstxt(8)="セブンのサラダチキンはマジで美味い。\n俺的にはハーブがおすすめ"
		tipstxt(9)="髪伸びてきたら邪魔よな"

		ProcessMessage

		SetDrawScreen hdximg@(iScene)
		ClearDrawScreen
		SetDrawMode DX_DRAWMODE_BILINEAR

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		DrawExtendGraph 0,0,BufWidth@,BufHeight@,hdximg@(ibg_load),FALSE

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph 0,0,hdximg@(ibg_load_pic),TRUE
		DrawExtendGraph 80,968,80+_progval,968+8,hdximg@(iprogbar),FALSE
		DrawGraph -16,413,hdximg@(ibg_tips),TRUE

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		ChangeFont"Meiryo":ChangeFontType DX_FONTTYPE_ANTIALIASING_4X4
		SetFontSize 16
		DrawString 80,928,_msg,GetColor(255,255,255)
		SetFontSize 24
		DrawString 214,495,tipstxt((gettime(6)/10)\10),GetColor(10,10,10)

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256-_alpha
		DrawBox 0,0,BufWidth@,BufHeight@,_color,TRUE

		SetDrawScreen DX_SCREEN_BACK
		ClearDrawScreen
		SetDrawBlendMode DX_BLENDMODE_NOBLEND
		DrawExtendGraph 0,0,DispWidth@,DispHeight@,hdximg@(iScene),TRUE

		ScreenFlip
		return

	#deffunc DrawLoadTransition int _color,int _time,int _flag
		if LowQuaFlag@==TRUE :return
		repeat _time+1
			if _flag==0 {
				setease 0,256,ease_linear
				DrawLoadScreen"しばらくお待ち下さい",0,getease(cnt,_time),_color
			} else {
				setease 256,0,ease_linear
				DrawLoadScreen"しばらくお待ち下さい",500,getease(cnt,_time),_color
			}
		loop
		return

#global