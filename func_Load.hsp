
#module _Load_

	//起動設定を読み込み
	#deffunc LoadCoreSettings str _filename
		exist _filename
		if strsize==-1 :return -1
		sdim coreBuf,strsize
		bload _filename,coreBuf

		dim DispWidth@
		DispWidth@=int(GetINIvalue(coreBuf,"Graphics","Display.Width"))
		dim DispHeight@
		DispHeight@=int(GetINIvalue(coreBuf,"Graphics","Display.Height"))
		dim BufWidth@
		BufWidth@=int(GetINIvalue(coreBuf,"Graphics","Buffer.Width"))
		dim BufHeight@
		BufHeight@=int(GetINIvalue(coreBuf,"Graphics","Buffer.Height"))
		dim FPSvalue@
		FPSvalue@=int(GetINIvalue(coreBuf,"Graphics","Refresh"))
		dim VsyncFlag@
		VsyncFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","Vsync"))
		dim FullScreenFlag@
		FullScreenFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","FullScreen"))
		dim BGAblurlev@
		sdim tmpstr:tmpstr=GetINIvalue(coreBuf,"Graphics","Movie.Blur")
		strrep tmpstr,"x",""
		BGAblurlev@=int(tmpstr)
		sdim BGAquality@
		switch GetINIvalue(coreBuf,"Graphics","Movie.Quality")
			case"high":BGAquality@="hq":swbreak
			case"mid":BGAquality@="mq":swbreak
			case"low":BGAquality@="lq":swbreak
			case"disable":BGAquality@="*":swbreak
		swend
		dim LowQuaFlag@
		LowQuaFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","Low.Quality"))
		dim CounterLev@
		CounterLev@=int(GetINIvalue(coreBuf,"Graphics","Counter.Level"))
		dim DrawCursorFlag@
		DrawCursorFlag@=ConvStrToBool(GetINIvalue(coreBuf,"Graphics","Use.CustomCursor"))
		sdim Acckey@
		Acckey@=GetINIvalue(coreBuf,"Hash","acckey")
		dim OfflinePass@
		OfflinePass@=ConvStrToBool(GetINIvalue(coreBuf,"Auth","Offline.Mode"))
		sdim httpproxy@
		httpproxy@=GetINIvalue(coreBuf,"Auth","Proxy")
		sdim httphost@
		httphost@=GetINIvalue(coreBuf,"Auth","Server")

	return

	//DXライブラリを初期化
	#deffunc InitDxlib
		SetOutApplicationLogValidFlag 0
		;SetUseDirectDrawDeviceIndex 0
		ChangeWindowMode FullScreenFlag@!1
		SetWindowStyleMode 7
		SetWindowSizeChangeEnableFlag TRUE,TRUE
		SetGraphMode DispWidth@,DispHeight@,32,60
		SetAlwaysRunFlag TRUE
		SetWaitVSyncFlag VsyncFlag@
		SetMainWindowText"Now Loading..."
		dxlib_Init
		if stat==-1 {
			dialog"グラフィック機能の初期化に失敗しました。"
		}
		SetMainWindowText"Sapphire"
		;SetUseGDIFlag TRUE

		//変数初期化
			dim KeyFlag@,256
			dim bfKeyFlag@,256
			dim CursorImgCount@
			dim CursorHideCount@
			dim bfCursorPosX@:dim bfCursorPosY@
			dim CursorPosX@:dim CursorPosY@
			dim PreloaderCount
	return

	//BASS Audio Libraryを初期化
	#deffunc InitBASSlib
		BASS_Init -1,44100,0,hwnd,0
		if stat==0 {
			dialog"オーディオ機能の初期化に失敗しました。"
		}
		BASS_SetConfig BASS_CONFIG_BUFFER,32
		if stat==0 {
			dialog"オーディオバッファの設定に失敗しました。"
		}
		BASS_SetConfig BASS_CONFIG_DEV_BUFFER,16
		if stat==0 {
			dialog"オーディオバッファの設定に失敗しました。"
		}
		dim64 hcommonchannel@:dim64 hcommonsample@
		LoadBASSsample"Data/bgm_Main.ogg",hcommonchannel@,hcommonsample@,BASS_SAMPLE_LOOP

		dim64 hcommonsech@:dim64 hcommonsesmp@
		LoadBASSsample"Data/snd_cancel.wav",hcommonsech@(sndCancel@),hcommonsesmp@(sndCancel@),NULL
		LoadBASSsample"Data/snd_change_0.wav",hcommonsech@(sndChange_0@),hcommonsesmp@(sndChange_0@),NULL
		LoadBASSsample"Data/snd_cursor.wav",hcommonsech@(sndCursor@),hcommonsesmp@(sndCursor@),NULL
		LoadBASSsample"Data/snd_decide.wav",hcommonsech@(sndDecide@),hcommonsesmp@(sndDecide@),NULL
		LoadBASSsample"Data/snd_error.wav",hcommonsech@(sndError@),hcommonsesmp@(sndError@),NULL
		LoadBASSsample"Data/snd_notification.wav",hcommonsech@(sndNotification@),hcommonsesmp@(sndNotification@),NULL
		LoadBASSsample"Data/snd_text.wav",hcommonsech@(sndText@),hcommonsesmp@(sndText@),NULL
		LoadBASSsample"Data/snd_Movement.wav",hcommonsech@(sndMovement@),hcommonsesmp@(sndMovement@),NULL
		LoadBASSsample"Data/voc_Lobby.wav",hcommonsech@(vocLobby@),hcommonsesmp@(vocLobby@),NULL
		LoadBASSsample"Data/voc_PartnerSelect.wav",hcommonsech@(vocPartnerSelect@),hcommonsesmp@(vocPartnerSelect@),NULL
		LoadBASSsample"Data/voc_Wait.wav",hcommonsech@(vocWait@),hcommonsesmp@(vocWait@),NULL
		LoadBASSsample"Data/voc_Buy.wav",hcommonsech@(vocBuy@),hcommonsesmp@(vocBuy@),NULL
	return

	//グローバル空間のグラフィックを作成
	#deffunc LoadCoreImages
		SetUsePremulAlphaConvertLoad TRUE
		SetDXArchiveExtension"bin"
		hdximg@(iScene)=MakeScreen(BufWidth@,BufHeight@,TRUE)
		dim hdximg_arr_cursor@,58
		repeat 58
			hdximg_arr_cursor@(cnt)=LoadGraph("Data/res_cursor/"+cnt+".png")
		loop
		dim hdximg_arr_preloader@,8
		repeat 8
			hdximg_arr_preloader@(cnt)=LoadGraph("Data/res_preloader/"+cnt+".png")
		loop
		dim hdximg_arr_preloader_2@,74
		repeat 74
			hdximg_arr_preloader_2@(cnt)=LoadGraph("Data/res_preloader_2/a"+strf("%04d",cnt)+".png")
		loop
		hdximg@(ibg_common_0)=LoadGraph("Data/bg_Main.ogv")
		hdximg@(ibg_common_1)=LoadGraph("Data/bg_Load.ogv")
		hdximg@(ibutton_backscene)=LoadGraph("Data/layer_common_back.png")
		hdximg@(ibutton_gameexit)=LoadGraph("Data/layer_common_exit.png")
		hdximg@(iStatus_Fragment)=LoadGraph("Data/Icon_Status_Fragment.png")
		hdximg@(iStatus_Ticket)=LoadGraph("Data/Icon_Status_Ticket.png")
		hdximg@(iStatus_Lilly)=LoadGraph("Data/Icon_Status_Lilly.png")
		hdximg@(ishape_commonbg)=LoadGraph("Data/layer_common_bg.png")
	return

	//グローバル空間の変数を初期化
	#deffunc InitCoreSceneMan
		dim FrameCount@
		dim coreCount@
		dim bfWheelVal@
		dim WheelVal@
		dim TotalScrollVal@
		dim FPSawaitLoop@
		dim InitLoadFlag@
		dim FatalErrorFlag@
		dim DebugStringFlag@
		sdim ExtraPlayInfo@
	return

	//シーンを初期化
	#deffunc InitScene int _sceneid
		dim SceneCount@
		dim SceneHalfCount@
		dim curSceneID@
		curSceneID@=_sceneid
	return

	//ムービーのエイリアスからIDを生成
	#defcfunc GetMovieAlias str _alias
		sdim tmpalias_lib:dim resid
		tmpalias_lib(0)="hexa","wave","ring","sq3d","cub0","li3d","cve0","tri0","cyb0","cir0","synp","wav1","city","dna*","cyb1","wav2"
		tmpalias_lib(16)="cir1","tri1","tri2","mn3d","cub1","cyb2","cyb3","glas","wav3","cyb4","tri3","ball","cve1","lght","cbci","spce"
		tmpalias_lib(32)="cir2"
		resid=-1
		repeat length(tmpalias_lib)
			if _alias==tmpalias_lib(cnt) :resid=cnt:break
		loop
	return resid

	//パートナーのアイコンを読み込み
	#deffunc LoadPartnerIcon
		exist"Data/res_char.bin"
		if strsize==-1 {
			return -1
		}

		dim CharMax@charicon
		sdim CharImgPath@charicon
		dim hCharImg@charicon
		dim hCharSil@charicon
		dim hCharFull@charicon
		sdim CharName@charicon
		sdim CharName_Eng@charicon
		sdim CharDesc@charicon
		sdim CharSkill@charicon
		dim CharPrice@charicon
		dim CharLev@charicon
		CharMax@charicon=int(GetINIvalue(coreBuf,"Partner","Reg.Max"))*2
		SetDXArchiveExtension"bin"
		repeat CharMax@charicon/2
			CharImgPath@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Path")
			hCharImg@charicon(cnt*2)=LoadGraph("Data/res_char/"+CharImgPath@charicon(cnt*2)+".png")
			hCharSil@charicon(cnt*2)=LoadGraph("Data/res_char/"+CharImgPath@charicon(cnt*2)+"_s.png")
			hCharFull@charicon(cnt*2)=LoadGraph("Data/res_char/"+CharImgPath@charicon(cnt*2)+"_full.png")
			CharName@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Name")
			CharName_Eng@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Name.eng")
			CharPrice@charicon(cnt*2)=GetINIvalue(coreBuf,"Partner","Char."+cnt+".Price")
			CharLev@charicon(cnt*2)=1
			CharImgPath@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Path")
			hCharImg@charicon(cnt*2+1)=LoadGraph("Data/res_char/"+CharImgPath@charicon(cnt*2+1)+".png")
			hCharSil@charicon(cnt*2+1)=LoadGraph("Data/res_char/"+CharImgPath@charicon(cnt*2+1)+"_s.png")
			hCharFull@charicon(cnt*2+1)=LoadGraph("Data/res_char/"+CharImgPath@charicon(cnt*2+1)+"_full.png")
			CharName@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Name")
			CharName_Eng@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Name.eng")
			CharPrice@charicon(cnt*2+1)=GetINIvalue(coreBuf,"Partner","Char."+cnt+"a.Price")
			CharLev@charicon(cnt*2+1)=1
		loop

		GetPartnerCharUnlockData
	return

	#deffunc DrawLoadScreen

		ProcessMessage

		SetDrawScreen DX_SCREEN_BACK
		ClearDrawScreen
		SetDrawScreen hdximg@(iScene)
		ClearDrawScreen

		SetDrawScreen DX_SCREEN_FRONT
		ClearDrawScreen
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph DispWidth@-369,DispHeight@-120,hdximg_arr_preloader@(PreloaderCount\8),TRUE

		PreloaderCount++

	return

	#enum global ID_STEP_GETLILLY	=	0
	#enum global ID_STEP_GETFRAGMENTS
	#enum global ID_STEP_FREE
	#enum global ID_STEP_MISSION
	#enum global ID_STEP_GETMAP

	#deffunc LoadChallengeModeMap str _mapfile
		exist _mapfile
		sdim mapbuf,strsize
		notesel mapbuf
		noteload _mapfile

		if GetINIvalue(mapbuf,"","#SIGN")!hash_core :return -1
		dim maxchapter@,int(GetINIvalue(mapbuf,"","#MAX.CHAPTER"))

		sdim chapterName@,64,maxchapter@
		sdim chapterAllRewards@,128,maxchapter@

		dim stepType@,maxchapter@,10
		sdim stepPrm@,64,maxchapter@,10
		sdim stepDesc@,64,maxchapter@,10

		sdim lib_missionuuid@
		sdim lib_mapuuid@
		dim lib_max

		dim curprogChapterID
		repeat notemax
			sdim INIkey:sdim INIvalue
			noteget tmpstr,cnt
			split tmpstr,"=",INIkey,INIvalue

			//チャプター定義
			repeat maxchapter@
				if INIkey=="#CHAPTER."+(cnt+1)+".----" :curprogChapterID=cnt
			loop

			//チャプターの詳細
			switch INIkey
				case"#NAME"		//マップ名
					chapterName@(curprogChapterID)=INIvalue
				swbreak
				case"#ALL.REWARDS"
					chapterAllRewards@(curprogChapterID)=INIvalue
				swbreak
			swend

			//ステップの定義
			repeat 10
				if INIkey=="#STEP."+cnt {
					sdim stepinfo
					split INIvalue,".",stepinfo
					if stepinfo(0)=="REWARDS"&stepinfo(1)=="LILLY" {		//LILLY獲得
						stepType@(curprogChapterID,cnt)=ID_STEP_GETLILLY
						stepPrm@(curprogChapterID,cnt)=stepinfo(2)
					}
					if stepinfo(0)=="REWARDS"&stepinfo(1)=="FRAGMENT" {		//音色の欠片
						stepType@(curprogChapterID,cnt)=ID_STEP_GETFRAGMENTS
						stepPrm@(curprogChapterID,cnt)=stepinfo(2)
					}
					if stepinfo(0)=="FREE" {		//なし
						stepType@(curprogChapterID,cnt)=ID_STEP_FREE
					}
					if stepinfo(0)=="MISSION" {	//ミッション
						stepType@(curprogChapterID,cnt)=ID_STEP_MISSION
						stepPrm@(curprogChapterID,cnt)=stepinfo(1)
						strrep stepinfo(2),"<br>","\n"
						stepDesc@(curprogChapterID,cnt)=stepinfo(2)
					}
					if stepinfo(0)=="REWARDS"&stepinfo(1)=="MAP" {	//楽曲獲得
						stepType@(curprogChapterID,cnt)=ID_STEP_GETMAP
						stepPrm@(curprogChapterID,cnt)=stepinfo(2)
					}
				}
				if INIkey=="#MAP.UUID" {
					sdim uuidspt
					split INIvalue,":",uuidspt
					lib_missionuuid@(lib_max)=uuidspt(0)
					lib_mapuuid@(lib_max)=uuidspt(1)
					lib_max++
				}
			loop

		loop

	return

	#defcfunc GetMapUUIDfromMissionUUID str _uuid
		sdim tmpstr
		repeat lib_max
			if lib_missionuuid@(cnt)==_uuid :tmpstr=lib_mapuuid@(cnt):break
		loop
		return tmpstr

#global