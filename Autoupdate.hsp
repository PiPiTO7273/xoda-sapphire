
#include"hspinet.as"
#include"mod_BinPkg.hsp"
#include"mod_VistaDialog.hsp"
#include"kernel32.as"
#include"hspext.as"

#define global DispWidth	1280
#define global DispHeight	720

#packopt hide 1

#module
	#deffunc UpdateScreenString str _msg
		color 255,255,255:boxf
		color 0,0,0:font"Yu Gothic UI",14
		pos 20,DispHeight-70:mes _msg
	return
#global

#module "inimodule"
#uselib "kernel32"
#cfunc WritePrivateProfileString "WritePrivateProfileStringA" sptr,sptr,sptr,sptr
#cfunc GetPrivateProfileString "GetPrivateProfileStringA" sptr,sptr,sptr,sptr,sptr,sptr
#cfunc GetFullPathName "GetFullPathNameA" sptr,sptr,sptr,sptr
#define MAX_PATH 512
#define NULL 0
#define nSize 256
;--------INIInit(INIモジュール初期化)---------
;p1 INIファイルのパス
#deffunc INIInit str inii_s
	sdim INI_FileName,strlen(inii_s):INI_FileName=inii_s
	sdim INI_LongName,MAX_PATH
	exist inii_s:ini_filesize = strsize
	inii_size = GetFullPathName(varptr(INI_FileName),MAX_PATH,varptr(INI_LongName),NULL)
	return inii_size
;--------INIGet(INIから読み込む)---------
;p1 セクション名
;p2 キー名
;refstr 取得した文字列
;戻り値　取得したサイズ
#defcfunc INIGet str inig_section,str inig_key
	mref inig_stat,64
	mref inig_refstr,65
	if(ini_filesize = -1){return 0}
	sdim INI_Section,strlen(inig_section):INI_Section=inig_section
	sdim INI_Key,strlen(inig_key):INI_Key=inig_key
	sdim lpReturnedString,nSize
	inig_stat   = GetPrivateProfileString(varptr(INI_Section),varptr(INI_Key),NULL,varptr(lpReturnedString),nSize,varptr(INI_LongName))
	inig_refstr = lpReturnedString
	return inig_refstr
;--------INISet(INIを書き換える)---------
;p1 セクション名
;p2 キー名（""でセクションを削除）
;p3 書き換える文字列（""でキーを削除）
#deffunc INISet str inis_section,str inis_key,str inis_str
	sdim INI_Section,strlen(inis_section):INI_Section=inis_section
	if (inis_key!=""){sdim INI_Key,strlen(inis_key):INI_Key=inis_key:INI_KeyPtr=varptr(INI_Key)}else{INI_KeyPtr=NULL}
	if (inis_str!=""){sdim INI_String,strlen(inis_str):INI_String=inis_str:INI_StringPtr=varptr(INI_String)}else{INI_StringPtr=NULL}
	ret = WritePrivateProfileString(varptr(INI_Section),INI_KeyPtr,INI_StringPtr,varptr(INI_LongName))
	return
#global

	screen 0,DispWidth,DispHeight,2,ginfo(20)/2-DispWidth/2,ginfo(21)/2-DispHeight/2-50
	title"puoro. updater"

	regkey 0,"SOFTWARE\\Microsoft\\Internet Explorer\\Main\\FeatureControl\\FEATURE_BROWSER_EMULATION",1
	dim tmpreg:tmpreg=11001
	setreg tmpreg,"SapphireLauncher.exe",0,4
	setreg tmpreg,"SapphireGame.exe",0,4
	setreg tmpreg,"hsp3.exe",0,4

	bcopy"core.ini","core.ini.update-tmp"
	INIInit"core.ini.update-tmp"
	dim curpatchver:curpatchver=int(INIGet("Common","Release"))

	netinit

	gsel 0,1

	pos 0,24:axobj IEcom,"Shell.Explorer.2",DispWidth,DispHeight-90-24
	IEcom("Silent")=1
	sdim weburl:weburl="http://puoro.ml/sapphire_releasenotes.html"
	strrep weburl,"%LAUNCHERDIR%",dir_cur
	IEcom->"Navigate" weburl

	comevent IEevent,IEcom,"{34A715A0-6587-11D0-924A-0020AFC7AC4D}",*webevent
	
	pos 20,DispHeight-40:winobj"msctls_progress32","",,$50000000,DispWidth-40-140,24
	hProgress=objinfo(stat,2)
	UpdateScreenString"アップデートを確認中・・・"
	sendmsg hProgress,$404,10
	sendmsg hProgress,$405

	objmode 2
	objsize 120,24
	pos DispWidth-140,DispHeight-40:button gosub"起動",*boot
	objenable 2,0

	objsize 100,24
	pos 0,0:button gosub"<< 戻る",*webback
	pos 100,0:button gosub"進む >>",*webforward
	pos 200,0:input weburl,DispWidth-300,24,0
	pos DispWidth-100,0:button gosub"移動",*webgo
	wait 25

	neturl"http://puoro.ml/build/"
	netrequest_get"sapphire_release_autoupdate.dat"
	repeat
		netexec res
		if res:break
		await
	loop
	sdim commonUpdateInfo
	dim Latestversion
	netgetv commonUpdateInfo
	sdim NoteLine
	notesel commonUpdateInfo
	sendmsg hProgress,$404,80/notemax
	repeat notemax
		notesel commonUpdateInfo
		noteget NoteLine,cnt
		sdim patchversion:sdim patchurl
		split NoteLine,">>",patchversion,patchurl,patchaction,patchtarget
		if int(patchversion)>curpatchver {
			UpdateScreenString"アップデートをダウンロード中・・・"
			//アップデート開始
			if patchaction=="COPY" {
				neturl getpath(patchurl,32)

				sdim httpheader:sdim httpheader_noteline
				netfileinfo httpheader,getpath(patchurl,8)
				notesel httpheader
				repeat notemax
					noteget httpheader_noteline,cnt
					if instr(httpheader_noteline,0,"Content-Length: ")!-1 :break
				loop
				dim httplength:httplength=int(strmid(httpheader_noteline,strlen("Content-Length: "),256))

				netrequest_get getpath(patchurl,8)
				repeat
					dim httpressize
					netsize httpressize
					;exist patchtarget
					UpdateScreenString "\""+getpath(patchtarget,8)+"\" をダウンロードしています。 ("+int(double(httpressize)/double(httplength)*100.0)+"%)"
					netexec res
					if res:break
					await
				loop
				UpdateScreenString"アップデートを適用中・・・"
				sdim binpkgraw,netgetv_size()
				netgetv_data binpkgraw
				bsave patchtarget,binpkgraw,netgetv_requestsize()
			}
			if patchaction=="MKDIR-ROOT" {
				sdim checkpath:checkpath=dir_cur+"\\"+patchtarget
				dirlist tmpstr,checkpath,5
				if stat==0 {
					mkdir patchtarget
				}
			}
			if patchaction=="MKDIR-2" {
				chdir patchurl
				sdim checkpath:checkpath=dir_cur+"\\"+patchtarget
				dirlist tmpstr,checkpath,5
				if stat==0 {
					mkdir patchtarget
				}
				chdir".."
			}
			if patchaction=="DELETE" {
				exist patchtarget
				if strsize!-1 :delete patchtarget
			}
			if patchaction=="DELETE-DIR" {
				RemoveDirectory patchtarget
			}
		}
		if Latestversion<int(patchversion) :Latestversion=int(patchversion)
		sendmsg hProgress,$405
	loop
	sendmsg hProgress,$404,10
	UpdateScreenString"しばらくお待ちください・・・"
	delete"core.ini.update-tmp"
	INIInit"core.ini"
	INISet"Common","Release",str(Latestversion)
	wait 50
	UpdateScreenString"ゲームを開始するには、「起動」ボタンを押してください。"
	clrobj 1,1

	objenable 2,1

	stop
*boot
	VistaDialog"Sapphire - グラフィック設定","ゲームの画質設定を行います。","次のプリセットから環境に適した設定を選択してください。","処理落ちが発生すると、正常にプレイできなくなる可能性があります。",101,"標準画質設定(720p)\n低画質設定、最低限のエフェクト\n高画質設定(1080p)、最高クオリティの演出",0,1
	switch stat
		case 101
			INISet"Graphics","Display.Width","1280"
			INISet"Graphics","Display.Height","720"
			INISet"Graphics","Low.Quality","FALSE"
			INISet"Graphics","Movie.Quality","mid"
		swbreak
		case 102
			INISet"Graphics","Display.Width","1024"
			INISet"Graphics","Display.Height","768"
			INISet"Graphics","Low.Quality","TRUE"
			INISet"Graphics","Movie.Quality","low"
		swbreak
		case 103
			INISet"Graphics","Display.Width","1920"
			INISet"Graphics","Display.Height","1080"
			INISet"Graphics","Low.Quality","FALSE"
			INISet"Graphics","Movie.Quality","high"
		swbreak
		case IDCANCEL:return:swbreak
	swend
	VistaDialog"Sapphire - グラフィック設定","ゲームの画質設定を行います。","画面モードを選択してください。","ウィンドウモードでプレイする場合、遅延や応答速度に影響する可能性があります。",101,"全画面モード\nウィンドウモード",0,1
	switch stat
		case 101
			INISet"Graphics","FullScreen","TRUE"
		swbreak
		case 102
			INISet"Graphics","FullScreen","FALSE"
		swbreak
		case IDCANCEL:return:swbreak
	swend
	exec INIGet("Common","Game")
	end

*webback
	IEcom->"GoBack"
	return

*webforward
	IEcom->"GoForward"
	return

*webgo
	IEcom->"Navigate" weburl
	return

*webevent
	if comevdisp(IEevent)==252 {
		weburl=IEcom("LocationURL")
		objprm 5,weburl
	}
	return