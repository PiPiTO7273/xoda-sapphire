
#include"hspinet.as"
#include"mod_BinPkg.hsp"

#packopt hide 1

#module
	#deffunc UpdateScreenString str _msg
		color 255,255,255:boxf
		color 0,0,0:font"Yu Gothic UI",14
		pos 20,30:mes _msg
	return
#global

#module "inimodule"
#uselib "kernel32"
#cfunc WritePrivateProfileString "WritePrivateProfileStringA" sptr,sptr,sptr,sptr
#cfunc GetPrivateProfileString "GetPrivateProfileStringA" sptr,sptr,sptr,sptr,sptr,sptr
#cfunc GetFullPathName "GetFullPathNameA" sptr,sptr,sptr,sptr
#define MAX_PATH 512
#define NULL 0
#define nSize 256
;--------INIInit(INIモジュール初期化)---------
;p1 INIファイルのパス
#deffunc INIInit str inii_s
	sdim INI_FileName,strlen(inii_s):INI_FileName=inii_s
	sdim INI_LongName,MAX_PATH
	exist inii_s:ini_filesize = strsize
	inii_size = GetFullPathName(varptr(INI_FileName),MAX_PATH,varptr(INI_LongName),NULL)
	return inii_size
;--------INIGet(INIから読み込む)---------
;p1 セクション名
;p2 キー名
;refstr 取得した文字列
;戻り値　取得したサイズ
#defcfunc INIGet str inig_section,str inig_key
	mref inig_stat,64
	mref inig_refstr,65
	if(ini_filesize = -1){return 0}
	sdim INI_Section,strlen(inig_section):INI_Section=inig_section
	sdim INI_Key,strlen(inig_key):INI_Key=inig_key
	sdim lpReturnedString,nSize
	inig_stat   = GetPrivateProfileString(varptr(INI_Section),varptr(INI_Key),NULL,varptr(lpReturnedString),nSize,varptr(INI_LongName))
	inig_refstr = lpReturnedString
	return inig_refstr
;--------INISet(INIを書き換える)---------
;p1 セクション名
;p2 キー名（""でセクションを削除）
;p3 書き換える文字列（""でキーを削除）
#deffunc INISet str inis_section,str inis_key,str inis_str
	sdim INI_Section,strlen(inis_section):INI_Section=inis_section
	if (inis_key!=""){sdim INI_Key,strlen(inis_key):INI_Key=inis_key:INI_KeyPtr=varptr(INI_Key)}else{INI_KeyPtr=NULL}
	if (inis_str!=""){sdim INI_String,strlen(inis_str):INI_String=inis_str:INI_StringPtr=varptr(INI_String)}else{INI_StringPtr=NULL}
	ret = WritePrivateProfileString(varptr(INI_Section),INI_KeyPtr,INI_StringPtr,varptr(INI_LongName))
	return
#global

	screen 0,400,120
	title"puoro. updater"

	INIInit"core.ini"
	dim curpatchver:curpatchver=int(INIGet("Launcher","Release"))
	sdim launcherpath:launcherpath=INIGet("Common","Launcher")

	netinit
	pos 20,60:winobj"msctls_progress32","",,$50000000,360,24
	hProgress=objinfo(stat,2)
	UpdateScreenString"アップデートを確認中・・・"
	sendmsg hProgress,$404,10
	sendmsg hProgress,$405
	wait 25

	neturl"http://puoro.ml/apiv2/"
	netrequest_get"core_Update.dat"
	repeat
		netexec res
		if res:break
		await
	loop
	sdim commonUpdateInfo
	dim Latestversion
	netgetv commonUpdateInfo
	notesel commonUpdateInfo
	sdim NoteLine
	sendmsg hProgress,$404,80/notemax
	repeat notemax
		noteget NoteLine,cnt
		sdim patchversion:sdim patchurl
		split NoteLine,">>",patchversion,patchurl,patchaction,patchtarget
		if int(patchversion)>curpatchver {
			UpdateScreenString"アップデートをダウンロード中・・・"
			//アップデート開始
			if patchaction=="COPY" {
				neturl getpath(patchurl,32)
				netrequest_get getpath(patchurl,8)
				repeat
					netexec res
					if res:break
					await
				loop
				UpdateScreenString"アップデートを適用中・・・"
				sdim binpkgraw,netgetv_size()
				netgetv_data binpkgraw
				bsave patchtarget,binpkgraw,netgetv_requestsize()
			}
		}
		if Latestversion<patchversion :Latestversion=patchversion
		sendmsg hProgress,$405
	loop
	sendmsg hProgress,$404,10
	sendmsg hProgress,$405
	UpdateScreenString"しばらくお待ちください・・・"
	INISet"Launcher","Release",str(Latestversion)

	exec INIGet("Common","Launcher")
	end