
#include"hsp3utf.as"

#module "inimodule"
#uselib "kernel32"
#cfunc WritePrivateProfileString "WritePrivateProfileStringA" sptr,sptr,sptr,sptr
#cfunc GetPrivateProfileString "GetPrivateProfileStringA" sptr,sptr,sptr,sptr,sptr,sptr
#cfunc GetFullPathName "GetFullPathNameA" sptr,sptr,sptr,sptr
#define MAX_PATH 512
#define NULL 0
#define nSize 256
;--------INIInit(INIモジュール初期化)---------
;p1 INIファイルのパス
#deffunc INIInit str inii_s
	sdim INI_FileName,strlen(inii_s):INI_FileName=inii_s
	sdim INI_LongName,MAX_PATH
	exist inii_s:ini_filesize = strsize
	inii_size = GetFullPathName(varptr(INI_FileName),MAX_PATH,varptr(INI_LongName),NULL)
	return inii_size
;--------INIGet(INIから読み込む)---------
;p1 セクション名
;p2 キー名
;refstr 取得した文字列
;戻り値　取得したサイズ
#defcfunc INIGet str inig_section,str inig_key
	mref inig_stat,64
	mref inig_refstr,65
	if(ini_filesize = -1){return 0}
	sdim INI_Section,strlen(inig_section):INI_Section=inig_section
	sdim INI_Key,strlen(inig_key):INI_Key=inig_key
	sdim lpReturnedString,nSize
	inig_stat   = GetPrivateProfileString(varptr(INI_Section),varptr(INI_Key),NULL,varptr(lpReturnedString),nSize,varptr(INI_LongName))
	inig_refstr = lpReturnedString
	return inig_refstr
;--------INISet(INIを書き換える)---------
;p1 セクション名
;p2 キー名（""でセクションを削除）
;p3 書き換える文字列（""でキーを削除）
#deffunc INISet str inis_section,str inis_key,str inis_str
	sdim INI_Section,strlen(inis_section):INI_Section=inis_section
	if (inis_key!=""){sdim INI_Key,strlen(inis_key):INI_Key=inis_key:INI_KeyPtr=varptr(INI_Key)}else{INI_KeyPtr=NULL}
	if (inis_str!=""){sdim INI_String,strlen(inis_str):INI_String=inis_str:INI_StringPtr=varptr(INI_String)}else{INI_StringPtr=NULL}
	ret = WritePrivateProfileString(varptr(INI_Section),INI_KeyPtr,INI_StringPtr,varptr(INI_LongName))
	return
#global


#packopt hide 1

exist"core.ini"
if strsize==-1 {
	dialog"ランチャーを起動できませんでした。\n\nエラー : 起動設定ファイルが見つかりません。",1,"エラー"
	end
}
INIInit"core.ini"

screen 0,320,320
title"起動設定"

syscolor 15:boxf

font"MS GOTHIC UI",20,1
color 0,0,0
pos 20,20:mes"こんにちは、"+sysinfo(1)+"さん"

font"MS GOTHIC UI",16,1
color 0,0,0
pos 16,60:mes"画面解像度"
objsize 130,20
if INIGet("Graphics","Display.Width")=="1920"&INIGet("Graphics","Display.Height")=="1080" :flag_1080p=1
if INIGet("Graphics","Display.Width")=="1280"&INIGet("Graphics","Display.Height")=="720" :flag_720p=1
if INIGet("Graphics","Display.Width")=="640"&INIGet("Graphics","Display.Height")=="480" :flag_480p=1
chkbox"1920x1080 (1080p)",flag_1080p
sendmsg objinfo(stat,2),$F4,$9
chkbox"1280x720 (720p)",flag_720p
sendmsg objinfo(stat,2),$F4,$9
chkbox"640x480",flag_480p
sendmsg objinfo(stat,2),$F4,$9
pos 160,60:mes"オプション"
if INIGet("Graphics","FullScreen")=="TRUE" :flag_fullscreen=1
if INIGet("Graphics","Vsync")=="TRUE" :flag_vsync=1
chkbox"フルスクリーンで起動する",flag_fullscreen
chkbox"垂直同期を有効にする",flag_vsync
pos 16,150:mes"ムービーの品質"
if INIGet("Graphics","Movie.Quality")=="high" :sel_mvqua=0
if INIGet("Graphics","Movie.Quality")=="mid" :sel_mvqua=1
if INIGet("Graphics","Movie.Quality")=="low" :sel_mvqua=2
if INIGet("Graphics","Movie.Quality")=="disable" :sel_mvqua=3
combox sel_mvqua,0,"高 (1920x1080)\n中 (1280x720)\n低 (640x480)\nムービーを表示しない"

objsize 120,30
pos 180,260:button"プレイ",*boot
stop

*boot
	sendmsg objinfo(0,2),$F0:flag_1080p=stat
	sendmsg objinfo(1,2),$F0:flag_720p=stat
	sendmsg objinfo(2,2),$F0:flag_480p=stat
	if flag_1080p :INISet"Graphics","Display.Width","1920":INISet"Graphics","Display.Height","1080"
	if flag_720p :INISet"Graphics","Display.Width","1280":INISet"Graphics","Display.Height","720"
	if flag_480p :INISet"Graphics","Display.Width","640":INISet"Graphics","Display.Height","480"
	if flag_fullscreen :INISet"Graphics","FullScreen","TRUE":else:INISet"Graphics","FullScreen","FALSE"
	if flag_vsync :INISet"Graphics","Vsync","TRUE":else:INISet"Graphics","Vsync","FALSE"
	if sel_mvqua==0 :INISet"Graphics","Movie.Quality","high"
	if sel_mvqua==1 :INISet"Graphics","Movie.Quality","mid"
	if sel_mvqua==2 :INISet"Graphics","Movie.Quality","low"
	if sel_mvqua==3 :INISet"Graphics","Movie.Quality","disable"
	exec INIGet("App","Path")
	end