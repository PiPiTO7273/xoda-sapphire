
#module _Layer_

	#deffunc DrawDebugString int posx,int posy,str msg
		SetDrawBlendMode DX_BLENDMODE_ALPHA,200
		DrawBox LogCharOffsetX+posx*LogChargridWidth,LogCharOffsetY+posy*LogChargridHeight,LogCharOffsetX+posx*LogChargridWidth+strlen(msg)*LogCharbgWidth,LogCharOffsetY+posy*LogChargridHeight+LogCharbgHeight,GetColor(0,0,0),TRUE
		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		DrawString LogCharOffsetX+posx*LogChargridWidth,LogCharOffsetY+posy*LogChargridHeight,msg,GetColor(255,255,255)
	return

	#deffunc OpenEditCommentDialog
		SetDrawScreen DX_SCREEN_FRONT
			SetDrawBlendMode DX_BLENDMODE_ALPHA,120
			DrawBox 0,0,DispWidth@,DispHeight@,0,TRUE
			SetDrawBlendMode DX_BLENDMODE_ALPHA,215
			DrawBox 0,DispHeight@/2-128,DispWidth@,DispHeight@/2+128,0,TRUE

			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			ChangeFont"Meiryo":SetFontSize 16:ChangeFontType DX_FONTTYPE_ANTIALIASING_4X4
			DrawStringXCenter 0,DispHeight@/2-64,"設定するコメントを入力してください。（最大24文字）",DispWidth@,$FFFFFF
			DrawStringXCenter 0,DispHeight@/2+64,"ENTER : 決定",DispWidth@,$2ECAD5
			
			SetDrawBlendMode DX_BLENDMODE_ALPHA,100
			DrawBox DispWidth@/2-256,DispHeight@/2,DispWidth@/2+256,DispHeight@/2+40,$FFFFFF,FALSE

			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			SetKeyInputStringColor $FFFFFF,$444444,$00E9C8,$00B99F,$00E9C8,$00E9C8,$00E9C8,$FFFFFF,$00E9C8,$00E9C8
			cfunc64v KeyInputString,DispWidth@/2-256+16,DispHeight@/2+10,24,varptr64(RegBadge@portalsync),FALSE
		RegBadge@portalsync=cnvatos(RegBadge@portalsync)
	return

	#deffunc OpenCommandMenu
		SetDrawScreen DX_SCREEN_FRONT
			SetDrawBlendMode DX_BLENDMODE_ALPHA,120
			DrawBox 0,0,DispWidth@,DispHeight@,0,TRUE
			SetDrawBlendMode DX_BLENDMODE_ALPHA,215
			DrawBox 0,DispHeight@/2-128,DispWidth@,DispHeight@/2+128,0,TRUE

			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			ChangeFont"Meiryo":SetFontSize 16:ChangeFontType DX_FONTTYPE_ANTIALIASING_4X4
			DrawStringXCenter 0,DispHeight@/2-64,"Accepting commands ...",DispWidth@,$FFFFFF
			DrawStringXCenter 0,DispHeight@/2+64,"ENTER : Run",DispWidth@,$2ECAD5
			
			SetDrawBlendMode DX_BLENDMODE_ALPHA,100
			DrawBox DispWidth@/2-256,DispHeight@/2,DispWidth@/2+256,DispHeight@/2+40,$FFFFFF,FALSE

			SetDrawBlendMode DX_BLENDMODE_ALPHA,256
			SetKeyInputStringColor $FFFFFF,$444444,$00E9C8,$00B99F,$00E9C8,$00E9C8,$00E9C8,$FFFFFF,$00E9C8,$00E9C8
			sdim acccmd,64
			cfunc64v KeyInputString,DispWidth@/2-256+16,DispHeight@/2+10,64,varptr64(acccmd),FALSE

			sdim acckey:sdim accprm
			split acccmd,"=",acckey,accprm

			switch getpath(acckey,16)
				case"edit.challenge.stepid"
					ChapterStepID@portalsync=int(accprm)
				swbreak
				case"edit.challenge.mapid"
					ChapterMapID@portalsync=int(accprm)
				swbreak
				case"set.default.songunlock"
					repeat int(GetINIvalue(coreBuf@_load_,"Playlist","Reg.Max"))
						tmpstr=GetINIvalue(coreBuf@_load_,"Playlist","ID."+cnt)
						split tmpstr,",",tmpstr(1),tmpstr(2),tmpstr(3)
						repeat 3
							if tmpstr(cnt+1)!"*" {
								RegChartUnlockFlag tmpstr(cnt+1),0
							}
						loop
					loop
				swbreak
				case"set.unlimited.songunlock"
					repeat int(GetINIvalue(coreBuf@_load_,"Playlist","Reg.Max"))
						tmpstr=GetINIvalue(coreBuf@_load_,"Playlist","ID."+cnt)
						split tmpstr,",",tmpstr(1),tmpstr(2),tmpstr(3)
						repeat 3
							if tmpstr(cnt+1)!"*" {
								RegChartUnlockFlag tmpstr(cnt+1),1
							}
						loop
					loop
				swbreak
				case"edit.ticket"
					NumTickets@portalsync=int(accprm)
				swbreak
				case"edit.fragments"
					NumFragments@portalsync=int(accprm)
				swbreak
				case"edit.lilly"
					NumLilly@portalsync=int(accprm)
				swbreak
				case"portal.sync"
					SendAllProfile
				swbreak
				case"exit"
					goto*Exit@
				swbreak
				case"open.url"
					exec accprm,16
				swbreak
				case"open.app"
					exec accprm,0
				swbreak
				case"set.default.partnerunlock"
					repeat CharMax@charicon
						CharUnlockFlag@charicon(cnt)=FALSE
						WebAPI_SendProfile"XODA.PARTNER.UNLOCK."+cnt,"0"
					loop
				swbreak
				case"set.drawdebugstring"
					DebugStringFlag@=int(accprm)
				swbreak
				case"goto.title"
					cfunc64v BASS_ChannelPause,hcommonchannel@
					LoadTitleFonts
					InitScene ID_TITLE
					StartTitle
				swbreak
			swend

	return

	//str	=	シーン名
	//int	=	fPS16 handle
	//int	=	アイテムの所持数を描画するフラグ
	//int	=	カーソルX座標
	//int	=	カーソルY座標
	#deffunc DrawDefaultStatus str _name,int _hfont,int _itemdrawflag,int _cposx,int _cposy
		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		DrawBox 0,BufHeight@-40,BufWidth@,BufHeight@,$272727,TRUE
		if _hfont==0 {
			DrawString 24,1051,_name,Getcolor(230,230,230)
		} else {
			DrawStringToHandle 24,1051,_name,GetColor(230,230,230),_hfont
		}

		if _itemdrawflag {
			if _hfont==0 {
				DrawString 458,1050,"x"+NumTickets@portalsync,Getcolor(255,255,255)
				DrawString 670,1050,"x"+NumFragments@portalsync,Getcolor(255,255,255)
				DrawString 814,1050,str(NumLilly@portalsync)+" Lilly",Getcolor(255,255,255)
			} else {
				DrawStringToHandle 458,1050,"x"+NumTickets@portalsync,Getcolor(255,255,255),_hfont
				DrawStringToHandle 670,1050,"x"+NumFragments@portalsync,Getcolor(255,255,255),_hfont
				DrawStringToHandle 814,1050,str(NumLilly@portalsync)+" Lilly",Getcolor(255,255,255),_hfont
			}

			SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
			DrawGraph 300,1048,hdximg@(iStatus_Ticket),TRUE
			DrawGraph 560,1048,hdximg@(iStatus_Fragment),TRUE
			DrawGraph 780,1048,hdximg@(iStatus_Lilly),TRUE
		}

		DrawGraph 1750,1040,hdximg@(ibutton_gameexit),TRUE
		if insquare(_cposx,_cposy,1750,1040,1750+150,1080) {
			SetDrawBlendMode DX_BLENDMODE_ALPHA,10
			DrawBox 1750,1040,1750+150,1080,$FFFFFF,TRUE
			if GetMouseInput()&MOUSE_INPUT_LEFT {
				goto*Exit@
			}
		}

		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		return

	#deffunc DrawSystemLayer
		//マウスカーソル
			cfunc64v GetMousePoint,varptr64(CursorPosX@),varptr64(CursorPosY@)
			if DrawCursorFlag@==TRUE|FullScreenFlag@==TRUE {
				if CursorHideCount@>FPSvalue@*2&curSceneID@==ID_PLAY {
					setease 256,0,ease_linear
					SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(CursorHideCount@-FPSvalue@*2,FPSvalue@/4)
				} else {
					SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				}
				DrawGraph CursorPosX@-32,CursorPosY@-32,hdximg_arr_cursor@(CursorImgCount@),TRUE
			}

			if bfCursorPosX@!CursorPosX@|bfCursorPosY@!CursorPosY@ {
				CursorHideCount@=0
			}
			bfCursorPosX@=CursorPosX@:bfCursorPosY@=CursorPosY@

		//デバッグ文字列
		if DebugStringFlag@ {
			ChangeFont"MS GOTHIC":SetFontSize 16
			ChangeFontType DX_FONTTYPE_NORMAL
			DrawDebugString 0,0,"Sapphire @debug"
			DrawDebugString 0,1,"HD "+BufWidth@+"x"+BufHeight@+" -> "+DispWidth@+"x"+DispHeight@
			DrawDebugString 0,2,"Server : "+LoginUserInfo@(ID_HOST)+" @\""+LoginUserInfo@(ID_NAME)+"\" & \""+LoginUserInfo@(ID_KEY)+"\""
			DrawDebugString 0,4,"FPS="+double(curFPS@)
			DrawDebugString 0,6,"C="+coreCount+" F="+FrameCount+" S="+SceneCount+" ,Level="+CounterLev@
			DrawDebugString 0,7,"Cursor ("+CursorPosX+","+CursorPosY+") "+GetMouseWheelRotVol()
			DrawDebugString 0,8,"Music Select ("+musicselect_ItemID@+","+musicselect_LevelID@+")"
			DrawDebugString 0,9,""+DispLogMsg@
		}

		//開発者用コマンド
		if KeyFlag@(KEY_INPUT_F11)==TRUE {
			OpenCommandMenu
		}

		//エラー検出
		if FatalErrorFlag@==TRUE {
			curSceneID=ID_FATALERROR
		}
	return

#global