
#module _title_

#enum ibg		=	0
#enum ilogo_0
#enum ilogo_1
#enum ilogo_2

#define logotime	4

#deffunc LoadTitleImages
	dim hdximg
	SetDXArchiveExtension"bin"
	hdximg(ibg)=LoadGraph("Data/bg_title.mp4")
	repeat 3
		hdximg(ilogo_0+cnt)=LoadGraph("Data/common_title_"+cnt+".png")
	loop
	return

#deffunc LoadTitleFonts
	return

#deffunc StartTitle
	dim SceneCount
	dim SceneTransCount:SceneTransCount=-1
	dim tmpscreencount
	return

#deffunc DrawTitle

	//カーソル位置補正
	dim CursorPosX:dim CursorPosY:dim MouseInput
	MouseInput=GetMouseInput()
	CursorPosX=int(double(CursorPosX@)*(double(BufWidth@)/double(DispWidth@)))
	CursorPosY=int(double(CursorPosY@)*(double(BufHeight@)/double(DispHeight@)))
	RawWheelVal=GetMouseWheelRotVol()
	RawWheelMove=0
	if RawWheelVal<0&bfRawWheelVal<0&RawWheelVal!bfRawWheelVal :RawWheelMove=RawWheelVal-bfRawWheelVal
	if RawWheelVal>0&bfRawWheelVal>0&RawWheelVal!bfRawWheelVal :RawWheelMove=RawWheelVal-bfRawWheelVal
	if (RawWheelVal>0&bfRawWheelVal<0)|(RawWheelVal<0&bfRawWheelVal>0) :RawWheelMove=RawWheelVal
	bfRawWheelVal=RawWheelVal
	PushButtonFlag=FALSE

	SetDrawMode DX_DRAWMODE_BILINEAR

	SetDrawBlendMode DX_BLENDMODE_NOBLEND
	DrawBox 0,0,BufWidth@,BufHeight@,$FFFFFF,TRUE

	if SceneCount>60*logotime*3 {
		setease 0,256,ease_linear
		SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount-60*logotime*3,30)
		DrawExtendGraph 0,0,BufWidth@,BufHeight@,hdximg(ibg),FALSE
	}

		//ロゴ
		repeat 3
			if SceneCount>60*logotime*cnt&SceneCount<60*logotime*(cnt+1) {
				tmpscreencount=SceneCount-60*logotime*cnt
				EaseAlpha=256
				if tmpscreencount<30 {
					setease 0,256,ease_linear
					EaseAlpha=getease(tmpscreencount,30)
				}
				if tmpscreencount>60*3-30 {
					setease 256,0,ease_linear
					EaseAlpha=getease(tmpscreencount-60*logotime+30,30)
				}
				SetDrawBlendMode DX_BLENDMODE_ALPHA,EaseAlpha
				DrawGraph 0,0,hdximg(ilogo_0+cnt)
			}
		loop

		if (GetMouseInput()&MOUSE_INPUT_LEFT)&(bfMouseInput&MOUSE_INPUT_LEFT)==FALSE&SceneTransCount==-1 {
			if SceneCount<60*logotime*3 {
				SceneCount=limit(SceneCount+60*logotime,0,60*logotime*3)
			} else {
				SceneTransCount=0
			}
		}

		if SceneCount<15 {
			setease 256,0,ease_linear
			SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneCount,15)
			DrawBox 0,0,BufWidth@,BufHeight@,0,TRUE
		}

		if SceneTransCount!-1 {
			setease 0,256,ease_linear
			SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneTransCount,15)
			DrawBox 0,0,BufWidth@,BufHeight@,0,TRUE
		}

		if SceneTransCount!-1 :AddCount SceneTransCount
		if SceneTransCount>20 {

			if RegName@portalsync==""|RegAuth@portalsync=="" {
				LoadSigninFonts
				LoadSigninImages
				StartSignin
				InitScene ID_SIGNIN
			} else {
				InitScene ID_LOBBY
				LoadLobbyImages
				InitLobbyFonts
				StartLobby
			}
		}

		if SceneCount==60*logotime*3 :PlayMovieToGraph hdximg(ibg),DX_PLAYTYPE_LOOP

		AddCount SceneCount

	bfMouseInput=MouseInput

	return

#global